label InterestingNumber_day1:

    # images
    image day1_railroad = '/interestingNumber/images/day1/day1_railroad.jpg'
    image day1_lake = '/interestingNumber/images/day1/day1_lake.jpg'
    image avtoda = '/interestingNumber/images/day1/avtoda1.jpg'

    $ backdrop = 'days'
    $ new_chapter(1, u'Интересный номер. День первый.')
    $ persistent.sprite_time = 'day'
    $ day_time()
    
    th 'Вокруг тьма.'
    th 'Тело немного болит, но главная проблема, что очень жарко...'
    th 'Стоп. Жарко?! Зимой?!'
    
    scene white with dissolve
    play sound sfx_intro_bus_transition

    'Я резко вскочил и открыл глаза. Меня ослепил яркий свет, я инстинктивно зажмурился.'
    
    scene bg int_bus with dissolve

    'Через время я потихоньку открыл глаза и сразу осмотрел место, где нахожусь.'

    stop sound

    'За окном было лето, а я находился, по-видимому, в Икарусе.'
    
    play music music_list['no_tresspassing'] fadein 0.5

    th 'Что за чёрт? Лето, Икарус. Где я?'
    iv '{i}Даже я не смогу тебе на это ответить. Я ничего не помню после того, как ты выпил вторую бутылку водки.{/i}'
    th 'И ты тут? Хоть что-то не поменялось.'
    'Тут мой взор упал на другие кресла. Там мирно спали…'
    lx 'Гошан, Санёк!'
    'Я подскочил их тормошить.'
    lx 'Вставайте! Мы в какой-то заднице!'
    cn 'Отстань, дай поспать, и выруби к чёрту печку. Жарко ужасно.'
    gh 'Согласен.'
    'Сквозь сон сказал Гошан.'
    lx 'Я не могу вырубить солнце, придурок!'
    cn 'Ха, ха. Смешно. Такое палящее солнце в декабре? Ха. Солнце… Солнце!?'
    'Саня также, как и я, резко вскочил с сиденья и был ослеплён.'
    cn 'Лёха, что за чёрт? Где мы?'
    lx 'Хороший вопрос. Сам не знаю. Знаю только то, что мы в Икарусе и попали в лето.'
    'Саня сел обратно на кресло и переваривал информацию.'
    'Я же начал будить Гошана.'
    lx 'Эй! Вставай, ты так долго спал, что уже лето наступило!'
    gh 'Какое нафиг лето? Дай поспать.'
    'Краем глаза я заметил, что Саня пристально смотрит на меня. Я повернулся к нему.'
    cn 'Лёха, что с твоим лицом? Тебе как будто семнадцать…'
    lx 'Что ты несёшь?'
    'И тут я получше осмотрел Саню. Передо мной сидел тот же Санёк, но лет семнадцати-восемнадцати. '
    lx 'Лучше просто не может быть. Нас переместили в лето, другой транспорт, так ещё и омолодили.'
    th 'Через небольшой промежуток времени Гошан изобразил такую же реакцию, как и мы.'
    gh 'И что делать будем-то?'
    lx 'Не знаю.'
    cn 'Так, первым делом надо снять тёплую одежду, а то помрём так. А потом выйдем на разведку.'
    'Мы все оперативно сняли одежду. Гошан пошёл к выходу, но остановился.'
    lx 'Ну чё встал?'
    gh 'Да страшно как-то, а вдруг там опасно?'
    lx 'Ну нас же трое, если что справимся. Да, Санёк?'
    'Саня лишь одобрительно кивнул.'
    
    scene bg ext_bus with dissolve

    'Мы вышли из Икаруса и начали осматриваться. Саня пошёл к воротам. Гошан решил осмотреть автобус, а я - всё остальное.'
    'Мне попался на глаза табличка на лобовом стекле автобуса.'
    th '410-й. Хм. {b}Интересный номер.{/b}'
    'Я бы и дальше смотрел на неё, но меня вывел из раздумий голос Сани.'
    
    scene bg ext_camp_entrance_day with dissolve
    play ambience ambience_camp_entrance_day fadein 2.0
    stop music fadeout 3.0

    cn 'Парни! Кто-то идёт. Вроде женщина.'
    'Мы все собрались у Икаруса и стали ждать.'

    play sound sfx_open_dooor_campus_2

    'Ворота скрипнули, и из-за них вышла девушка лет двадцати пяти в пионерской форме.'

    stop sound

    mtp 'Здравствуйте, мальчики! Вы Алексей, Александр и Георгий, верно?'
    'Мы дружно кивнули головой.'
    mtp 'А я - Ольга Дмитриевна. Ваша вожатая!'
    lx 'Эээ… Приятно познакомиться.'
    cn 'Рад знакомству.'
    gh 'Угу.'
    'Пока вожатая что-то говорила, мы недоумённо смотрели друг на друга.'
    th 'Так, главное - не паниковать. Надо подыграть этой вожатой, а потом разузнать всё об этом месте.'
    lx 'Благодарим вас за встречу.'
    cn 'Да, спасибо, но мы очень устали после этой поездки. Вы бы могли показать, где мы будем жить?'
    'Ольга Дмитриевна, видимо, не ожидала такого вопроса и встала немного в ступор, но почти сразу пришла в себя.'
    mt 'Конечно, пойдёмте, ребятки, будем вас распределять.'

    scene bg ext_clubs_day with dissolve
    play ambience ambience_camp_center_day fadeout 1.5 fadein 1.5
    play music music_list['my_daily_life'] fadein 1.5

    'Мы зашли за ворота, а через некоторое время вышли к небольшому зданию.'
    lx 'Ольга Дмитриевна, а это что за здание?'
    'Вожатая остановилась.'
    mt 'Это - клубы, тут у нас кибернетики работают, если будет интересно, можете потом записаться.'
    lx 'Угу, конечно…'
    th 'Нет.'

    scene bg ext_square_day with dissolve

    'Пройдя клубы, мы вышли на площадь. Нас встретил странный памятник.'
    gh 'Сань, а это кто?'
    cn 'А шут его знает. Руководитель какой-то.'
    lx 'Угу, ему лучше бы роботами руководить.'
    'После осмотра площади я заметил, что ещё не встретил людей. Это заметили и другие из нашей компании.'
    lx 'Ольга Дмитриевна, мы же, надеюсь, не одни в лагере? Где все?'
    'Ольга Дмитриевна посмотрела на меня и посмеялась.'
    mt 'Конечно нет! Просто сейчас обед, все в столовой. Увы, вы не успели к нему. Придётся ждать до ужина.'
    gh 'Столовая? А можете сразу показать, где она?'
    'Мы с Саней переглянулись с улыбкой. Ольга кивнула и, махнув рукой, повела нас за собой.'

    scene bg ext_dining_hall_away_day with dissolve

    'Через время мы вышли к зданию, которое издалека можно охарактеризовать как обычную столовку.'
    mt 'Вот и наша столовая. Завтрак, обед, ужин - всё тут.'
    'Мы одобрительно кивнули.'
    'После осмотра самого важного, по мнению Гошана, места, нас повели дальше.'

    scene bg ext_house_of_mt_day with dissolve

    'Пройдя возле одинаковых домов, где, скорее всего, живут пионеры, мы вышли к треугольному домику, который был просто {b}залит{/b} сиренью.'
    cn 'Ух, ну и запашок. В нос так и бьёт.'
    gh 'Согласен. Интересно, где жить будем?'
    lx 'Сейчас и узнаем.'

    stop ambience fadeout 1.5

    'Вожатая отперла домик и пригласила нас зайти.'

    scene bg int_house_of_mt_day with dissolve
    play music music_list['get_to_know_me_better'] fadeout 1.5 fadein 2.0
    play sound sfx_open_door_1

    'Мы зашли внутрь и перед нами предстал обычный домик. Парочку плакатов, две кровати, тумбочки, полочки. Ничего странного.'

    stop sound

    mt 'Так, пионеры, подождите секунду. Я посмотрю, куда вас разместить. А пока возьмите форму, она на кровати.'
    'Ольга Дмитриевна взяла планшет с каким-то листком и вышла из дома.'
    'Мы подошли к кровати.'
    lx 'Ну что, товарищи пионэры, будем примерять одежду?'
    cn 'А куда мы денемся?'
    'Через минуту мы уже были одеты в свою новую форму. В ту же секунду открылась дверь. Это была Ольга.'
    mt 'Оделись? Хорошо.'
    mt 'Так, Саша и Гоша, вы будете в одном домике. Вот ключи.'
    'Она передала Сане ключи с номером четырнадцать.'
    mt 'А ты, Лёша, будешь жить со мной. Прости, другие домики заняты.'
    th 'Замечательно. {w}Просто великолепно. {w}Хрен знает где, вокруг пионеры, какой-то лагерь, так еще и жить буду в одном доме с вожатой.'
    th 'М-да, что ещё? {w}Метеорит упадёт? {w}Маньяк, который будет всех рубить? {w}Или появятся монстры какие-нибудь? {w}Хотя нет, может всё сразу?!'
    lx 'Хорошо. Где моя кровать?'
    mt 'Левая. Она уже заправлена.'
    lx 'Угу. Мы свободны? Хотелось бы прогуляться по лагерю до ужина.'
    'Вожатая, не отвечая, кивнула головой и быстро занялась своими делами.'
    
    scene bg ext_house_of_mt_day with dissolve
    stop music fadeout 2
    play ambience ambience_camp_center_day fadein 1.5
    play sound sfx_close_door_1

    'Наша компашка вышла из домика.'

    stop sound

    gh 'Ну и куда пойдём?'
    lx 'Даже не знаю. Куда глаза ведут?'
    cn 'Так не пойдёт. Идём на площадь, я там карту видел. Поможет нам сориентироваться.'

    scene bg ext_houses_day with dissolve
    play music music_list['everyday_theme'] fadein 2

    'Мы отправились на площадь.'
    'Идя по дорожке к площади, пока Гошан и Саня смеялись над своей формой и всем происходящим, я задумался.'
    th 'А зачем мы здесь? Кто нас сюда отправил?'
    th 'А может мы всё-таки умерли? Тогда что это за место - ад или рай?'
    iv '{i}Будешь долго об этом думать - с ума сойдешь. Почему дружки твои не парятся над этим?{/i}'
    th 'Потому что.'
    iv '{i}Ответ хороший. Ты всю жизнь плыл по течению, делая псевдовыборы.{/i}'
    th 'К чему ты это?'
    iv '{i}К тому, что именно в этот момент лучше плыть по течению. Пока что.{/i}'

    scene bg ext_square_day with dissolve

    'Мы вышли на площадь и подошли к стенду с картой.'
    # stop sound
    lx 'Хорошо. Вот только, как нам это все запомнить?'
    cn 'Никак. Я сфотографирую на телефон.'
    gh 'Откуда у тебя телефон?'
    cn 'Я, в отличие от вас, ношу его с собой всегда.'
    'Саня достал телефон и сфоткал карту.'
    cn 'Ну вот и отлично. Куда пойдём?'

    $ disable_all_zones()
    $ set_zone('square', 'Square_day1')
    $ set_zone('camp_entrance', 'BusStop_day1')
    $ set_zone('library', 'WentToLibrary_day1')
    $ show_map()

    label Square_day1:

        $ day1_wentToSquare = True

        scene bg ext_square_day

        lx 'Пойдём получше рассмотрим площадь.'
        'Мы отошли от стенда.'
        'На площади уже была, какая-то пионерка. Она подметала площадь.'
        th 'Какое-то бесполезное занятие.'
        'Девушка стояла ко мне спиной, поэтому я не мог полностью её разглядеть.'
        lx 'Привет.'
        'Я думал, она дёрнется от неожиданности, но нет. Она плавно развернулась и мило улыбнулась. И только теперь я смог полностью её рассмотреть.'
        'Красивая девушка с золотыми волосами, которые были заплетены в косу. Глаза были как море, в котором хотелось вечно плавать.'
        iv '{i}Может быть, ты что-нибудь уже скажешь?{/i}'
        th 'Чёрт. Надеюсь, она не подумает, что я тормоз.'
        lx 'Эм. Да, привет ещё раз. Мы тут, вот, новенькие. Ничего толком не знаем. Даже отряда. Ольга нам об этом забыла сказать.'
        slp 'Здравствуй. Мы? Ты же тут один.'
        lx 'Ну, видишь тех парней?'
        'Я показал рукой на Саню и Гошана.'
        lx 'Вот это мои друзья. Вместе приехали.'
        th 'Скорее телепортировались.'
        slp 'А, да, да. Мне о вас рассказывали. Я - Славяна, но можешь называть, Славя.'
        'Она мило улыбнулась.'
        lx 'Славя? Приятно. Меня - Алексей, но можешь звать Лёха.'
        sl 'Рада познакомится.'
        sl 'Может, ты позовёшь своих друзей, а то я твоё имя знаю, а их - нет.'
        'Я кивнул и жестом руки подозвал Гошана и Саню.'
        lx 'Вот. Познакомься. Это Саня.'
        'Я показал на Санька, тот кивнул и, улыбнувшись, сказал:'
        cn 'Да, это я. По крайней мере был им с утра.'
        'Мы с Гошаном даже скулой не шевельнули, а вот Славя засмеялась.'
        'Видимо, такая реакция понравилась Сане, и он улыбнулся шире. Я начал говорить снова.'
        lx 'А вот это Гошан.'
        'Гошан ничего не сказал, просто поднял руку в знак приветствия.'
        sl 'Замечательно, рада знакомству с вами. А меня зовут Славя. Я помощница вожатой, Ольги Дмитриевны. Кстати, вы в нашем отряде, первом.'

        stop music fadeout 2.0

        th 'Она помощница вожатой? Хм, может, её попросить провести нам экскурсию?'

        menu:
            'Попросить провести экскурсию':
                $ slPoints += 1
                $ day1_wentWithSlavya = True
                jump goWithSlavya_day1

            'Не просить об экскурсии':
                jump notGoWithSlavya_day1


        label goWithSlavya_day1:

            play music music_list['get_to_know_me_better'] fadein 2
            scene bg ext_square_day

            lx 'Славя, а ты не могла бы провести для нашей компании экскурсию по лагерю?'
            'Славя смотрела на меня пару секунду, а потом, улыбнувшись, сказала.'
            sl 'Конечно могу, только метлу отнесу. Никуда не уходите, скоро приду.'
            'Славя взяла метлу и быстро скрылась по дорожке в неизвестное направление.'
            lx 'Ну, вот я и нашел нам проводника, не придётся таскаться самим.'
            'Я посмотрел на своих товарищей.'
            cn 'Да, а то мы бы так до вечера ходили.'
            gh 'Экскурсия - это хорошо, но поскорее бы ужин. Чувство, что три дня не ел.'
            'Мы с Саней в унисон начали смеяться. Гошан непонимающе на нас смотрел.'
            gh 'Что вы ржёте?'
            'Отсмеявшись, Саня сказал.'
            cn 'Эх, Гошан. Может смениться всё что угодно, но твоё чувство голода всегда остаётся прежним.'
            gh 'Да ну вас.'
            'Он обиженно отвернулся.'
            lx 'Да ладно тебе, Гош, мы же просто шутим. Не обижайся.'
            'Поколебавшись, Гошан повернулся к нам.'
            gh 'Ладно, что на вас обижаться.'
            'Он улыбнулся. Через время пришла Славя.'
            sl 'Простите, что так долго, проблемки небольшие были. Пойдёмте?'
            'Мы дружно кивнули. Славя начала экскурсию.'

            scene bg ext_aidpost_day with dissolve

            'Сначала она привела нас к небольшому деревянному зданию.'
            'Осмотрев его, я ухмыльнулся. Здание было хорошо видно, а на его крыше был флаг с красным крестом.'
            lx 'Ну, тут мы сами могли бы понять. Обычный мед. пункт.'
            'Славя кивнула.'
            sl 'Пригодится, если что-то случится или нужны будут медикаменты.'

            scene bg ext_library_day with dissolve

            'Дальше Славя вывела нас к библиотеке.'
            'Пока она говорила, что-то про эту постройку, я подошел к Сане и Гошану.'
            lx 'Надо бы потом сюда сходить. Вдруг найдём там что-нибудь. Даты, к примеру.'
            'Они кивнули.'
            'Рассказав все, что Славя хотела, мы двинули дальше.'

            scene bg ext_playground_day with dissolve

            'Пройдя по дорожкам, наша группа вышла к чему-то наподобие спортплощадки.'
            sl 'Вот, мальчики. Тут у нас зачастую играют в футбол или просто занимаются спортом.'
            'Гошан скривился, а Саня равнодушно осмотрелся.'
            sl 'Ещё есть кружки. Так что, если захотите - сможете записаться.'
            th 'Ага, спасибо, мне есть куда потратить время помимо этого.'
            lx 'Э, да, будем знать. Ну что, идем дальше?'
            'Пионерка кивнула и направилась в другую сторону.'

            scene bg ext_boathouse_day with dissolve
            play ambience ambience_boat_station_day fadeout 1.5 fadein 1.5

            'В какой-то момент, домики сменились деревьями, а через секунду мы вышли к воде.'
            th 'Хм, лодки, домишко какой-то. Типо причала?'
            'Я покосился на Славю, и она, уловив мой взгляд, поспешила объяснить, где мы.'
            sl 'Думаю, вы поняли, что это наш причал. Иногда, мы пользуемся лодки, чтобы добраться до того острова.'
            'Она указала рукой в сторону островка, маячащего на горизонте.'
            th 'Интересно, надо взять на заметку это место, точно пригодиться.'
            'Вдруг заговорил Саня.'
            cn 'Так, мы можем пользоваться лодками, когда хотим?'
            'Славя вздохнула и покачала головой.'
            sl 'Нет, только с разрешения вожатой и только по важным причинам…'
            'Вдруг она посмотрела по сторонам и перешла на шёпот.'
            sl 'Но, на самом деле, ими пользуются и без ведома. Вы главное не попадайтесь и говорите, что поплывете, хотя бы мне, чтобы никто не попал в неловкую ситуацию.'
            sl 'Хорошо?'
            'Мы были приятно удивленны.'
            th 'Опа-опа, а я думал, что она прям вся такая правильная. Значит ошибался, неплохая девушка.'
            iv '{i}М-да.{/i}'
            'Что?'
            iv '{i}Ты ж понимаешь, что если она была бы и полностью \"правильная\" это бы не значило, что она плохая?{/i}'
            th 'Ну, может быть. Поживем увидем.'
            iv '{i}Да ну тебя.{/i}'
            'Голос затих, и я посмотрел на Славю.'
            sl 'Ребят, вы, конечно, простите, но я сейчас покажу последнее место и уйду по делам. Вы же не против?'
            cn 'Всё нормально. Ты и так показала много всего.'
            lx 'Да, мы уже за это благодарны.'
            'А Гошан лишь кивнул в знак солидарности. Славя улыбнулась.'
            cn 'Ну, тогда хорошо. Пойдемте, тут недалеко.'
            'И мы стали отходить от пристани.'

            scene bg ext_musclub_day with dissolve
            play ambience ambience_camp_center_day fadeout 1.5 fadein 1.5

            'Пройдя по парочке дорожек, мы вышли к небольшому зданию с большими окнами.'
            'Внутрь мы не зашли, но через окна я понял, что это что-то наподобие музыкального клуба.'
            'Насчет этой достопримечательности Славя не распиналась. И так все понятно.'
            'Когда наша проводница закончила, она развернулась к нам.'
            sl 'Вот мы и закончили, можете сами ещё погулять, а я побежала по делам. Увидимся!'
            'Тут я вспомнил, что так и не узнал про людей в нашем отряде.'
            lx 'Славя, постой, а кто в нашем отряде?'
            'Она остановилась и повернулась. На её лице появилась хитрая улыбка.'
            sl 'Это вы потом узнаете.'
            'После этих слов она убежала.'

            stop music fadeout 2

            lx 'И что это, нахрен, было?'
            'Я посмотрел на своих товарищей.'
            cn 'Тайны и загадки. Ладно, разберёмся потом.'
            'Мы сели на лавку. И тут я вскочил.'

            play music music_list['everyday_theme'] fadein 2

            lx 'Есть идея. Двигаем на площадь, там расскажу.'

            jump tryingToGetOut_day1


        label notGoWithSlavya_day1:

            th 'Да ну нет. Зачем человека загружать. Сами всё посмотрим.'
            lx 'Ладно. Спасибо за информацию. Ещё раз рад знакомству.'
            'Славя лишь мило улыбнулась и продолжила подметать площадь.'
            'Мы ушли с площади на тропинку.'
            lx 'Ладно, надо по-быстрому пройтись по территории. Погнали.'

            scene bg ext_houses_day with dissolve

            'После этих слов, мы вышли на дорожку с домиками. Я завёл с Саней разговор о том, что происходит в этом лагере и не заметил, как Гошан от нас отстал.'

            stop music fadeout 2

            lx 'Саня, стоп. Мы Гошана потеряли.'
            'Саня повернул голову назад.'
            cn 'Хм. И вправду. Ладно, пойдём обратно, может, встретим его.'
            'Мы развернулись и пошли по своим следам.'

            play music music_list['you_won_t_let_me_down'] fadein 2

            'Через время мы заметили Гошана, а рядом с ним рыжую девушку.'
            'Сразу было видно, что рыжая была чем-то недовольна, а Гошан не мог ничего сказать. То ли не знал, что говорить, то ли, по своему обычаю, стеснялся.'
            cn 'Ну и что делать будем? Поглядим на это шоу и посмеёмся или спасать пойдём?'
            lx 'Дай подумать.'
            iv '{i}Лёха, это твой лучший друг. Ближе этих людей у тебя никого нет. Неужели ты бросишь его в беде?{/i}'
            th 'М-да.'

            menu:
                'Помочь Гошану':

                    $ dvPoints += 1
                    $ day1_helpedGosha = True
                    jump HelpedGosha_day1

                'Смотреть на шоу':

                    jump NotHelpedGosha_day1


            label HelpedGosha_day1:

                scene bg ext_houses_day

                th 'Ладно. Надо его спасать.'
                'Я направился к Гошану. Подходя, я начал слышать, что говорит рыжая.'
                dvp 'Эй. Ты вообще слушаешь? Ответить не можешь, что ли?'
                'Гошан лишь переминался с ноги на ногу и смотрел в землю. Я решил вмешаться в этот монолог.'
                lx 'Эй. Чего ты к нему пристаёшь?'
                'Её взор перешёл на меня. Я продолжил говорить.'
                lx 'Гошан, отомри. Что, при виде красавицы сразу язык проглотил?'
                'После этих слов я заметил, что на лице девушке возникли две эмоции - злость и смущение, но она быстро поборола вторую. Пока она думал, что мне сказать, я смог её рассмотреть.'
                'Симпатичная девушка с медно-рыжими волосами. Она бы не отличалась от других пионеров, если бы не её форма. Галстук на кисти, нижняя часть рубашки завязана в бант ниже груди. Бунтарка.'
                'Осмотр мой прервался, когда девушка всё-таки нашла, что сказать.'
                dvp 'Ты. Да ты кто такой вообще?!'
                'Я сложил руки на груди.'
                lx 'Друг этого человека.'
                'Я кивнул на Гошана. Он в это время уже пришёл в себя и подошёл ко мне.'
                dvp 'Хм. Друг твой? Молчаливый он какой-то. Может, ты на вопрос ответишь?'
                'Я почесал затылок.'
                lx 'Ну давай попробую.'
                'Рыжая уже остыла и говорила нормально.'
                dvp 'Это вы новенькие?'
                'Я немного улыбнулся, в какой-то степени, всё-таки, приятно, что кто-то хочет узнать о тебе информацию.'
                dvp 'Я сказала что-то смешное?'
                'Она снова начала заводиться.'
                lx 'Нет, ничего. Да, мы новенькие. Ещё вопросы?'
                'Я решил показать себя крутым.'
                iv '{i}Воу, воу. Не залей тут всех своей крутостью.{/i}'
                th 'Да ну тебя.'
                'Рыжая на мои слова лишь хмыкнула и, развернувшись, ушла.'

                stop music fadeout 2

                'Я посмотрел на Гошана.'
                lx 'Ну и чё ты отстал от нас?'
                'Гошан почесал репу.'
                gh 'Да я на немного остановился, чтоб окрестности осмотреть, а тут она.'
                'Я глянул на дорожку куда ушла рыжая.'
                lx 'А что хотела?'
                gh 'Ну, кто мы, откуда и куда. А ты меня знаешь, мне с девушками сложно. Вот я и стоял, молчал. Она уже сердиться начала...'
                'Я его перебил.'
                lx 'А тут я. Ладно. Фигня, но вот только из-за неё ничего нормально посмотреть не успеем теперь.'
                'Гошан лишь кивнул. Я, похлопав его по плечу, пошёл обратно к Сане.'
                cn 'Ну как?'
                'Я лишь махнул рукой.'
                'Мы сели на лавочку. И тут я вскочил.'

                play music music_list['take_me_beautifully'] fadein 2

                lx 'Есть идея. Двигаем на площадь, там расскажу.'
                
                jump tryingToGetOut_day1


            label NotHelpedGosha_day1:

                scene bg ext_houses_day

                th 'Да ну. Гошану надо научиться самому справляться с проблемами. Что я, мамка ему?'
                'Было принято решение наблюдать за происходящим.'
                cn 'Эх, жаль, плохо слышно, о чём она говорит.'
                'Я кивнул.'
                'Значит будем смотреть немое кино.'
                'Мы заржали. Рыжая сначала о чём-то говорила, а Гошан просто молчал. Потом девушка, видимо, начала злиться. Если бы мы были рядом, то наверняка бы видели дым из её ушей.'
                'Гошан всё продолжал молчать, смотря в землю. Рыжая не выдержала и, ничего не говоря, ушла от Гошана.'
                cn 'Быстро как-то.'
                lx 'Да, как думаешь, когда вторая серия будет?'
                'Мы начали смеяться, из-за чего нас заметил Гошан.'
                'Осмотревшись, он пошёл к нам.'
                gh 'Вы были тут рядом и не помогли?!'
                'Гошан был похож на ту рыжую.'
                lx 'Мы подумали, что ты сам сможешь справиться.'
                'Сказал я, продолжая смеяться.'
                gh 'Вы же знаете, как мне сложно с девушками! А вы...'

                stop music fadeout 2

                'Гошан погрустнел и сел на скамейку.'
                lx 'Гошан, да ладно тебе. В следующий раз мы поможем. Кто же знал, что она такой будет?'
                'Гошан посмотрел на нас изучающим взглядом.'
                gh 'Ловлю на слове.'
                'Он заулыбался.'
                'Мы сели рядом с ним. И тут я вскочил.'

                play music music_list['take_me_beautifully'] fadein 2

                lx 'Есть идея. Двигаем на площадь, там расскажу.'

                jump tryingToGetOut_day1


    label BusStop_day1:

        $ day1_wentToBusStop = True

        scene bg ext_square_day

        lx 'Может пойдём к остановке?'
        'Саня и Гошан кивнули. Мы направились в сторону выхода из лагеря.'

        scene bg ext_bus with dissolve
        play ambience ambience_camp_entrance_day fadeout 1.5 fadein 1.5

        'Быстро пройдя клубы, наша компания вышла к нужному нам месту.'
        'Перед нами предстал, вроде, тот же автобус, но рядом с ним были инструменты и какой-то мужик, выносящий коробки из багажного отделения. Я заметил среди инструментов гитару.'
        th 'Надо бы узнать, куда несут инструменты. Хотелось бы немного побренчать на гитарке.'
        'Саня ткнул меня в бок. Я повернул голову к нему.'
        cn 'Смотри-ка. Какая-то странная девчонка там крутится.'

        stop music fadeout 2

        'Он кивнул в сторону девушки, которую до это я почему-то не замечал.'
        'Она бегала у коробок и инструментов, что-то говорила мужику, но что, нам не было слышно. Выглядела она и вправду необычно.'
        'Её волосы были аквамаринового цвета и доходили чуть ли не до земли.'

        play music music_list['so_good_to_be_careless'] fadein 2

        'Пока мы стояли и смотрели за всем происходящим, аквамариновая девчонка нас заметила и направилась в нашу сторону.'
        'Не доходя до нас, она выдала пулемётную очредь из слов.'
        mip 'Ой, мальчики, привет! Вы новенькие, да? Я вот Мику, честно-честно...'
        'Она хотела сказать что-то ещё, но я её прервал.'
        lx 'Рад знакомству. Да, мы новенькие. Я Алексей, а это Гоша и Саня.'
        'Я показал на своих товарищей.'
        'Саня улыбнулся и помахал рукой, а Гошан просто кивнул.'
        mi 'Я так рада знакомству! Новые лица - это всегда хорошо!'
        'В этот момент грузчик окликнул девушку.'
        mi 'Ой, ребята, а вы не можете мне помочь? Мне нужно коробки и инструменты перенести.'
        'Я почесал затылок.'
        lx 'Ну, мы никуда не торопимся. Конечно поможем.'
        # 'Меня поддержал Саня.'
        cn 'Да. А то, как такая красивая и хрупкая девушка будет всё это нести?'
        'Мику засмущалась.'
        mi 'Ну, я же не одна.'
        'Саня её перебил.'
        cn 'Верно, ведь теперь тебе помогаем мы!'
        'Он широко улыбнулся.'
        'Я смотрел на него со смесью удивления, раздражения и зависти.'
        iv '{i}Что? Завидно, что друг так быстро девушку в краску вогнал, а ты не можешь?{/i}'
        th 'Да ну тебя. У меня ещё всё впереди.'
        iv '{i}Ага. Точно, точно.{/i}'
        'Мой разговор с внутренним голосом прервал Гошан.'
        gh 'Эй. Лёха, что встал? Пошли, бери инструменты.'
        'Я, помотав головой, двинулся к инструментам.'
        'Взяв гитару в одну руку, а тарелки от барабанной установки - в другую, я пошёл за Мику.'

        scene bg ext_musclub_day with dissolve
        play ambience ambience_camp_center_day fadeout 1.5 fadein 1.5

        'Пройдя по нескольким дорожкам, мы вышли к зданию с большими окнами.'
        'Мику поставила коробку и, достав ключ из кармана, открыла дверь.'

        play ambience ambience_music_club_day fadeout 1.5 fadein 1.5
        scene bg int_musclub_day with dissolve
        play sound sfx_open_door_1

        'Мы зашли внутрь.'

        stop sound

        mi 'Вот, поставьте сюда коробки, а в тот угол инструменты. Я потом сама всё расставлю.'
        'Расставив все вещи, наша компания села на пол.'
        mi 'Мальчики, спасибо! Что бы я без вас делала? Спасибо огромное!'
        'Я махнул рукой.'
        lx 'Да ладно. Если что, зови. Всегда поможем.'
        'Саня и Гошан одобрительно кивнули.'
        mi 'Даже не знаю, что бы вам в награду дать?'
        mi 'Ах, знаю. У меня чай есть с печеньем. Сейчас всё сделаю. Вы пока сидите, отдыхайте.'
        'Я хотел возразить, но не успел, потому что Мику уже убежала за дверь, которая, наверно, ведёт в подсобку.'
        'Скоро она вернулась с кружками, полными чая, и печеньем.'
        mi 'Вот, мальчики, угощайтесь.'
        gh 'Спасибо. А то, до ужина далеко, хоть там можно голод перебить.'
        'Мы с Мику слегка улыбнулись, а Саня чуть не подавился чаем от смеха.'
        'Попивая чай, я гипнотизировал гитару, что не осталось без внимания Мику.'
        mi 'Лёша, ты умеешь играть на гитаре? Если да, то можешь взять и сыграть. Я не против.'
        'Я посмотрел на Мику, а потом на гитару.'

        stop ambience fadeout 1.5
        stop music fadeout 1.5

        th 'А почему бы и нет?'
        'Я встал и подошёл к гитаре.'
        'Взяв инструмент, я начал его настраивать. Через несколько минут всё было готово.'
        lx 'Заказы будут или играть что захочу?'
        'Пацаны молчали.'
        mi 'Лучше сыграй то, что знаешь, а то я скажу что-нибудь, а ты сыграть хорошо не сможешь.'
        'Подумав, что играть, я решил, что сыграю {b}\"Вставить навание и музыку\"{/b}.' # TODO: музыка

        # TODO: сцена с игрой на гитаре

        play music music_list['so_good_to_be_careless'] fadein 2

        'Когда я закончил, Мику радостно захлопала.'
        mi 'Очень хорошо! Есть небольшие проблемки, но их легко исправить, а так, просто отлично!'
        'Мне было лестно это слышать.'
        mi 'Лёшенька, а ты не хотел бы записаться в музклуб?'
        th 'Хм. Неплохая идея, но надо ли?'

        menu:
            'Записаться в клуб':
                $ miPoints += 1
                $ day1_attendMusicClub = True
                jump AttendMusicClub_day1

            'Отказаться от вступления':
                jump NotAttendMusicClub_day1

        label AttendMusicClub_day1:

            th 'Ладно, чем чёрт не шутит? Да и к тому же, не буду же я тут всё время проводить? Зато на гитаре смогу играть.'
            iv '{i}Причём рядом с красивой девушкой!{/i}'
            th 'Да я не из-за этого записываюсь.'
            iv '{i}Мне то не ври.{/i}'
            th 'Да ну тебя.'
            lx 'Почему бы и нет? Записывай.'
            'Видимо, Мику не ожидала такого ответа и, сильно обрадовавшись, куда-то убежала.'
            'Не прошло и секунды, как она вернулась с каким-то листком.'
            mi 'Вот, напиши имя тут и распишись тут.'
            'Я взял тетради и ручку и посмотрел на листок. Это был список участников. Там были записаны Мику и некая Алиса.'
            th 'Ну, теперь тут будет ещё и Лёха Наумов.'
            'Написав свое имя и расписавшись, я отдал листок и ручку Мику. Она повернула голову в сторону парней.'
            mi 'А вы не хотите записаться?'
            'Гошан отрицательно помотал головой, а у Саня откуда-то появилась страсть делать комплименты женскому полу, которую я раньше не замечал.'
            cn 'Эх. Прости. Я, конечно, был бы не против побыть с тобой подольше, но куда друзья, туда и я.'
            'Он был горд своей фразой, а Мику опять засмущалась.'

            scene bg ext_musclub_day with dissolve
            play sound sfx_open_door_1

            'Попрощавшись с Мику, мы поставили чашки и вышли на улицу.'

            stop sound
            play music music_list['everyday_theme'] fadeout 1 fadein 2

            'Пройдя до ближайшей скамейки, мы присели отдохнуть. Я посмотрел на Саню.'
            lx 'И что это было?'
            'Саня непонимающе посмотрел на меня.'
            cn 'Ты о чём?'
            'Я сверлил его взглядом.'
            lx 'Странно ведёшь себя. Уже какой раз комплимент Мику отвешиваешь, хотя раньше такого я не замечал.'
            'Саня усмехнулся.'
            cn 'Да ладно тебе, Лёх. Когда ещё будет возможность узнать, насколько я хорош в этом деле?'
            'Я выдохнул.'
            lx 'Ладно, Дон Жуан недоделанный. Пойдёмте на площадь, у меня идея есть.'

            scene bg ext_square_day with dissolve

            jump tryingToGetOut_day1

        label NotAttendMusicClub_day1:
            
            scene bg int_musclub_day with dissolve

            th 'Всё же нет. Парни сюда не будут ходить, а одному, хоть и с такой девушкой...'
            th 'Но и отказывать ей как-то не хочется. Мда, ну и ситуация.'
            lx 'Даже не знаю. Я тут толком ничего не видел. Наверно, пока что откажусь. Ты же не обидишься?'
            'Мику на секунду погрустнела, но потом снова вернула прежнее лицо.'
            mi 'Да ничего. Можешь заходить, когда захочешь. А я помогу с недочётами в игре, если у тебя будет желание.'
            'Она повернула голову в сторону парней.'
            mi 'А вы не хотите записаться?'
            'Гошан отрицательно помотал головой, а у Саня откуда-то появилась страсть делать комплименты женскому полу, которую я раньше не замечал.'
            cn 'Эх. Прости. Я, конечно, был бы не против побыть с тобой подольше, но куда друзья, туда и я.'
            'Он был горд своей фразой, а Мику опять засмущалась.'

            scene bg ext_musclub_day with dissolve
            play sound sfx_open_door_1

            'Попрощавшись с Мику, мы поставили чашки и вышли на улицу.'

            stop sound
            play music music_list['everyday_theme'] fadeout 1 fadein 2

            'Пройдя до ближайшей скамейки, мы присели отдохнуть. Я посмотрел на Саню.'
            lx 'И что это было?'
            'Саня непонимающе посмотрел на меня.'
            cn 'Ты о чём?'
            'Я сверлил его взглядом.'
            lx 'Странно ведёшь себя. Уже какой раз комплимент Мику отвешиваешь, хотя раньше такого я не замечал.'
            'Саня усмехнулся.'
            cn 'Да ладно тебе, Лёх. Когда ещё будет возможность узнать, насколько я хорош в этом деле?'
            play sound sfx_inhale
            'Я выдохнул.'
            stop sound fadeout 0.5
            lx 'Ладно, Дон Жуан недоделанный. Пойдёмте на площадь, у меня идея есть.'

            scene bg ext_square_day with dissolve
            $ oston += 1
            jump tryingToGetOut_day1

    label Library_day1:

        $ day1_wentToLibrary = True

        scene bg ext_square_day
        
        lx 'Предлагаю пойти в храм знаний.'
        
        scene bg ext_houses_day with dissolve
        
        'Пока наша компашка продвигалась к месту назначения, Саня решил завести разговор.'
        cn 'А что мы вообще будем искать в библиотеке? Понятно, что информацию, но какую именно?'
        'Я почесал затылок.'
        lx 'Ну, где мы, какая сегодня дата хотя бы. Вот это всё.'
        'Саня кивнул, а Гошану было все равно, он все пыхтел и махал на себя руками.'
        
        play sound sfx_inhale
        
        gh 'Господи, почему так жарко? Когда уже будет эта библиотека?'
        
        stop sound
        
        'Я посмотрел на карту.'
        lx 'Совсем недалеко. Потерпи, нам недалеко.'
        'Гошан недовольно забурчал себе под нос.'
        
        stop music fadeout 2
        scene bg ext_library_day with dissolve
        
        'Я не соврал, пройдя ещё немного, мы вышли к, так называемой, библиотеке.'
        cn 'Ну, я чего-то такого и ожидал.'
        lx 'Заходим?'
        'Гошан ничего не ответив, быстро зашел в здание.'
        'Мы последовали за ним.'
        
        scene bg int_library_day with dissolve
        play sound sfx_open_door_1
        play ambience ambience_library_day fadeout 1.5 fadein 1.5
        
        'Наша компания полностью зашла в Храм знаний и пыли.'

        stop sound
        play music music_list['smooth_machine'] fadein 2
        
        'В библиотеке пахло, как... в библиотеке.'
        'Я быстро осмотрелся. Советские флаги, плакаты и, скорее всего, такая же литература.'
        th 'Надеюсь, тут будут интересные книги. Например, об истории этого лагеря.'
        'Но помимо всего увиденного, было чувство, что тут прошёл ураган. Где-то на полу валялись книги, где-то те самые плакаты.'
        'Я посмотрел на своих товарищей.'
        cn 'Может, тут просто давно никого не было?'
        'После Саниных слов откуда-то из другой части библиотеки раздался раздражённый голос.'
        voice 'В смысле никого не было?!'
        'После этого из-за полок вышли две девушки и я смог их рассмотреть.'
        'Одна была в очках, с синими волосами, а её взгляд смотрел на нас, как будто мы враги народа, которые и устроили этот беспорядок.'
        'Вторая же девушка была с фиолетовыми волосами и, в отличие от её подруги, просто стеснительно смотрела куда-то в пол.'
        'Девушка в очках продолжила.'
        mzp 'Вы кто такие? Говорите что нужно и идите куда шли, и так проблем много.'
        'Она больше не сверлила нас взглядом и перешла к уборке бардака.'
        lx 'Ну, нам бы книгу взять. Есть какая-нибудь об истории этого лагеря?'
        'Очкастая снова посмотрела на нас.'
        mzp 'Может и есть, не знаю. И искать я её не буду, пока не уберусь.'
        th 'Это намёк на то, чтобы ей помогли?'
        'Вдруг фиолетоволосая подала голос.'
        unp 'Я, ну, то есть, мы были бы очень благодарны, если бы вы помогли.'
        'После своих слов она покраснела ещё сильнее.'
        th 'А стоит ли тратить время и помогать им?'

        menu:
            'Помочь с уборкой':
                $ unPoints += 1
                $ day1_helpedLena = True
                jump HelpedLena_day1

            'Отказаться помогать':
                jump NotHelpedLena_day1


        label HelpedLena_day1:

            scene bg int_library_day

            th 'Ладно, может, если помочь этой очкастой, то она найдёт нужную мне книгу?'
            iv '{i}Хвала корысти!{/i}'
            th 'Ну, тип того.'
            lx 'Ладно, давайте мы вам поможем.'
            'Очкастая подняла на меня глаза.'
            mzp 'Ого, вот спасибо. Рак на горе свистнул?'
            th 'Мы ей помогать, а она ещё и острит.'
            'Я хотел уже всё ей высказать, но она меня опередила.'
            mzp 'Ладно уж. Пока убирайтесь тут, а я пойду поищу книги, которые вам нужны. Так сказать, произведём бартер.'
            'Я почесал подбородок.'
            lx 'Хорошо. А где веник и другие вещи для уборки?'
            'Очкастая указала на угол, где лежало всё, что нам нужно.'
            lx 'Кхм. Кстати, а как звать-то вас?'
            'Где-то из-за полок книг мы услышали ответ.'
            mzp 'Я - Женя.'
            'Фиолетоволосая аккуратно подняла голову.'
            unp 'А я - Лена.'
            # 'После этого она снова вернулась к работе.'
            'Быстро распределив задачи между нашей группой, мы приступили к их выполнению.'
            'Хоть бардак и был ужасный, но общими усилиями мы смогли с ним быстро управиться.'
            lx 'Фух, вроде всё.'
            'Я сел на первый попавшийся стул.'
            mz 'Я тоже. Вот, нашла какой-то журнал об этом лагере. Только вот на кой тебе он, непонятно, но я в чужие дела не лезу. Нужен, значит есть зачем.'
            # 'Она положила на стол журнал.'
            lx 'Хм. Интересно.'
            'Я подозвал Саню и Гошана к себе.'
            'Полистав журнал, мы толком ничего не нашли. Единственное, что мы поняли, это то, что администрация очень любит себя хвалить.'
            'В этой книжонке были прописаны только Советские награды, похвальные грамоты и всё в этом духе.'
            cn 'Мда. Информации выше крыши. Даже дат нет.'
            'Дат и вправду нигде не было.'
            gh 'Не, ну, мы хотя бы знаем точно, что находимся в СССР.'
            lx 'Не густо. Зря только убирались. Может, спросить у Лены про время?'
            'Я завертел головой в поиске Лены, но её уже не было в библиотеке.'
            th 'Чёрт, уже свинтила куда-то. Может, Женя что-нибудь скажет.'
            'Я повернул голову в сторону Жени.'
            lx 'Слушай, Женя. Что-то у меня из головы вылетело, а какой сейчас год и месяц?'
            'Женя оторвалась от полок и развернулась к нам. Она смотрела на меня, как на придурка.'
            mz 'Совсем перегрелся? Хотя и понятно, июль выдался жарким. Иди к медсестре лучше, а то так скоро имя своё забудешь.'
            th 'Ну хоть не послала и ладно. Зато теперь мы знаем, что сейчас июль. Так себе информация, но хоть что-то.'
            lx 'Да, Жень, ты права. Как-нибудь загляну к ней. Ладно, мы пошли, удачи.'
            mz 'Угу. Идите, идите.'

            stop music fadeout 2
            scene bg ext_library_day with dissolve
            play ambience ambience_camp_center_day fadeout 1.5 fadein 1.5
            play sound sfx_open_door_1

            'Мы вышли из библиотеки.'

            stop sound

            'Всей троицей мы хлопнулись на ближайшую скамейку.'
            'Немного посидев, я поймал хорошую мысль.'

            play music music_list['take_me_beautifully'] fadein 2

            lx 'Хм. Парни, у меня есть неплохая идея. Только давайте сначала на площадь. Там всё объясню.'

            scene bg ext_square_day with dissolve
            $ libri += 1
            jump tryingToGetOut_day1


        label NotHelpedLena_day1:
            scene bg int_library_day

            th 'Да ну нафиг. Мы вместо уборки можем что-нибудь полезнее сделать. Хотя, некрасиво получается. Надо как-то выкрутиться.'
            lx 'Кхм. Это, извините, мы бы помогли, но у нас ещё дела. Мы только на пару секунд за книгой хотели забежать. Я как-нибудь загляну ещё раз. Удачи.'
            'И, чтобы не ждать реакции девушек, я вытолкнул парней из библиотеки.'

            stop music fadeout 2
            scene bg ext_library_day with dissolve
            play ambience ambience_camp_center_day fadeout 1.5 fadein 1.5
            play sound sfx_open_door_1

            lx 'Да ну эту уборку.'

            stop sound

            gh 'Да, мне тоже не очень понравилась эта идея. Хотя, я бы посидел ещё в теньке.'
            'Саня молчал и пристально глядел в ближайшие кусты. Я уже хотел его спросить, что он там увидел, но он меня незаметно остановил.'
            cn 'Показалось, наверное. Ладно, пойдёмте.'
            'После этих слов он мне подмигнул и незаметно кивнул на кусты.'
            th 'Неужели там кто-то есть? Ладно, скоро об этом узнаем.'
            'Мы специально пошли поближе к кустам и как только поравнялись с ними, Саня молниеносно прыгнул в их сторону.'
            'Через пару мнгновений мы услышали его довольный возглас.'

            play music music_list['went_fishing_caught_a_girl'] fadein 2

            cn 'Ха. Поймал. Теперь не убежишь, шпион.'
            'Он вышел из кустом с девочкой в красной футболкой с гордой надписью {b}СССР{/b} и такими же красными волосами.'
            cn 'Вот, следила за нами. Думала, я тебя не заметил?'
            'Он, ухмыляясь, смотрел на девчонку. Так как Саня зажал ей рот, она могла лишь бубнить и пытаться вырваться из его железной хватки.'
            lx 'Так, мелкая, смотри. Тебе открывают рот, а ты не кричишь и не пытаешься убежать, хорошо?'
            'Девчонка кивнула. Саня убрал руку с её рта.'
            'Несколько секунд она молчала и изумленно смотрела на нас, но потом всё-таки подала голос.'
            usp 'Вы... вы кто такие?!'
            'Саня улыбнулся.'
            cn 'Хах. Я уж подумал, что ты немая.'
            'Гошан на эти слова хихикнул. Мелкая продолжила.'
            usp 'Вы новенькие, да?'
            'Я кивнул.'
            'Она быстро переменилась в лице, теперь ей было любопытно.'
            usp 'А как звать вас?'
            lx 'Я - Лёха, это Гошан, а поймал и держит тебя - Саня. А как тебя звать, шпион?'
            'Довольная улыбка появилась у неё на лице.'
            usp 'Да не шпион я. Меня Ульяной звать. Может, отпустите уже? Не бойтесь, не убегу.'
            'Саня расцепил свою хватку и выпустил Ульяну.'
            us 'Блин, быстрый ты, Саня, и зоркий. Разведчик, что ли?'
            'Саня покачал головой и как-то по теплому улыбнулся.'
            cn 'Не разведчик, так, на полставки.'
            'Ульяна, видимо, не поняла, что это шутка и изумленно уставилась на него.'
            us 'Вот это дааа! Я так и думала. Обычный человек не заметил бы меня.'
            'Сами того не заметив, мы улыбнулись.'
            lx 'Слушай, а ты ведь прячешься, верно? Это ты устроила бардак в библиотеке?'
            'Ульяна заулыбалась, её брала гордость за свой поступок.'
            us 'Вообще, я не этого хотела, но получилось даже лучше. Моей целью была Лена. Вы же не знаете её, да?'
            lx 'Угу.'
            us 'Это так фиолетоволосая.'
            'Хм. Надо запомнить. Лена, значит.'
            us 'Вообще, я хотела ей показать кузнечика, она их боиться. Ну и пока бегала за ней по библиотеке, немного бардака устроила.'
            cn 'Хах. Хороша, ничего не скажешь. А ты как думаешь, а, Лёх?'
            th 'Ладно над очкастой, смешно, конечно. Но вот зачем над той тихоней, Леной прикалываться? Как-то неправильно.'

            menu:
                'Согласиться':
                    $ usPoints += 1
                    $ day1_agreedWithUlyana = True
                    jump AgreedWithUlyana_day1

                'Не согласится':
                    jump NotAgreedWithUlyana_day1


            label AgreedWithUlyana_day1:

                scene bg ext_library_day

                iv '{i}Да ладно тебе, ты же её видел, с ней все нормально. Ульяна же её не травмировала. Как по мне, очень даже смешно.{/i}'
                th 'Может ты и прав.'
                'Я почесал подбородок.'
                lx 'Да, неплохо ты там набедокурила. А главное, что этой очкастой всё убирать.'
                'Гошан поддержал меня смешком.'
                us 'Эту очкастую, кстати, зовут Женя.'
                cn 'Хм. Мне больше нравится Жужелица.'
                'Мы все дружно засмеялись.'
                us 'А вы крутые. Надо как-нибудь вместе что-нибудь сделать. Но сейчас мне надо по делам, увидимся.'
                'После этих слов она, словно ракета, стартанула в кусты и исчезла.'
                gh 'Вот это ракета.'
                'Саня кивнул.'
                lx 'Ладно, давайте выйдем из этих зарослей.'

                stop music fadeout 2

                'Мы вышли из кустов и, заприметив скамейку, сели на неё.'
                'Немного посидев, я поймал хорошую мысль.'

                play music music_list['take_me_beautifully'] fadein 2

                lx 'Хм. Парни, у меня есть неплохая идея. Только давайте сначала на площадь. Там все объясню.'

                scene bg ext_square_day with dissolve

                jump tryingToGetOut_day1


            label NotAgreedWithUlyana_day1:

                scene bg ext_library_day

                th 'Нет, всё-таки это неправильно. Зачем над девушкой издеваться?'
                iv '{i}Мда. Какие мы правильные, даже повеселиться не можешь.{/i}'
                th 'Это разве смешно? Я так не считаю.'
                'Я нахмурился.'
                lx 'Это по-вашему смешно? Издеваться над зашуганной как минимум некрасиво.'
                'У всех сползла улыбка.'
                cn 'Да ладно тебе, Лёх. Это же безобидная шутка.'
                'Саню поддержал Гошан.'
                gh 'Да, Лёха. С ней же ничего не случилось.'
                'Но я не слушал их и продолжил своё причитание.'
                lx 'Тебе совсем не стыдно? А если бы над тобой так посмеялись?'
                'Ульяна зло смотрела на меня.'
                us 'Да ну тебя, зануда. Вот друзья твои всё понимают, а ты правильного из себя строишь. Ну и иди к Леночке свой, пожалей её.'
                'Эти слова меня кольнули и я начал злиться.'
                lx 'Да ты что себе позволяешь, а?'
                'Я направился в сторону Ульяны, но на моём пути встал Саня.'
                cn 'Что-то ты разошёлся.'
                'Он повернул голову в сторону мелкой.'
                cn 'Это, ты иди по своим делам. Были рады познакомиться.'

                stop music fadeout 3

                'Ульяна кивнула и как ракета улетела в кутсы. Саня повернул голову ко мне.'
                cn 'Пойдём, присядем на скамейку, чтобы ты отошёл немного.'
                'Мы вышили из кустом и, заприметив скамейку, сели на неё.'
                'Пока я успокаивался, меня осенила хорошая мысль.'

                play music music_list['take_me_beautifully'] fadein 2

                lx 'Парни, у меня есть неплохая идея. Двигаем на площадь, там все расскажу.'

                jump tryingToGetOut_day1


    label tryingToGetOut_day1:

        scene bg ext_square_day with dissolve

        'Через время мы вышли на площадь.'
        lx 'Так, парни. У меня есть план.'
        'Саня и Гошан вопросительно посмотрели на меня.'
        lx 'Мы же не пробовали слинять отсюда.'
        cn 'А смысл? Думаешь, если мы сюда попали, нас так легко выпустят?'
        gh 'Согласен с Саней, но всё же, попытка не пытка.'
        'Я потёр руки.'
        lx 'Ну вот и отлично.'
        'Развернувшись спиной к Генде, я заявил.'
        lx 'Идём к остановке. Будем выбираться пешком.'

        scene bg ext_clubs_day with dissolve

        'И мы отправились к месту, откуда появились в этом лагере.'
        'Пройдя клубы, я задумался.'
        th 'Странно, мы так и не видели других парней нашего отряда. Неужели, наша компания одна на весь отряд? Ладно, потом узнаем, если не выберемся, конечно.'
        'Мы подошли к воротам.'

        scene bg ext_no_bus with dissolve
        play ambience ambience_camp_entrance_day fadeout 1.0 fadein 1.5
        stop music fadeout 2.0

        'Переглянувшись, вы вышли за территорию лагеря.'
        'Всё было точно также, только автобус уехал.'
        cn 'Ну, либо тут и вправду был водитель, который уехал, либо автобус просто исчез.'
        'Я почесал затылок.'
        lx 'Ладно, нам от этого не тепло и не холодно.'

        'Я вышел на дорогу. Постояв немного и посмотрев по сторонам, я заявил:'
        lx 'Идём туда.'
        'И направил руку на горизонт дороги.'
        lx 'Идём прямо по дороге, так не потеряемся.'

        scene bg ext_road_day with dissolve
        play ambience ambience_ext_road_day fadeout 1.5 fadein 1.5

        'И, кивнув самому себе, я двинул вперёд, а Саня и Гошан поспешили за мной.'
        'Пока я шёл, смотря на красивые пейзажи природы, опять ушёл в свои мысли.'
        th 'Я точно хочу отсюда уходить? Да, тут красивее, теплее, и не только в плане температуры, люди другие. Хоть и встретили мы пока далеко не весь лагерь. В них нет той серости. Но тут как-то всё не реально. Это вызывает подозрение.'
        'Я остановился на передышку, ребята остановились прямо позади.'
        'После тяжёлого вздоха, Саня произнёс:'
        cn 'Лёх, мы и метра не прошли, а чувство, что несколько километров.'
        'Я вздрогнул.'

        play music music_list['trapped_in_dreams'] fadein 1.5

        th 'Не прошли и метра?!'
        'Я в страхе повернулся к нему.'
        lx 'Ты что несёшь?! Как это?! Мы же уже долго идём!'
        'Я сорвался на крик от нервов.'

        scene bg ext_camp_entrance_day with dissolve

        'Саня отошёл в сторону, и я увидел совсем недалеко проклятые ворота лагеря.'
        'Я сел на землю, не зная, что сказать. Саня похлопал меня по спине. Гошан согнулся в три погибели и пытался отдышаться.'
        'Я повернулся к лесу.'

        scene bg ext_road_day with dissolve

        iv '{i}Красиво тут, а ты сбежать хочешь. Куда вернуться-то? В ту тьму? Надо радоваться, а ты...{/i}'
        th 'Что я? Тут всё как в сказке. Пейзажи как картины. Нереально всё это.'
        iv '{i}Но ведь ты это можешь потрогать, почувствовать, увидеть. Разве с нереальным так можно?{/i}'
        
        scene bg ext_no_bus with dissolve

        'Тут Саня меня толкнул в плечо.'
        cn 'Идея есть. Давай Гошан тут останется, а мы дальше пройдём. Посмотрим, что будет.'
        'Я встал с земли и посмотрел на Гошана.'
        gh 'Да, хорошая идея, я пока отдышусь. Идите.'
        'Мы кивнули и снова пошли по дороге.'

        scene bg ext_road_day with dissolve

        'Отойдя недалеко от Гошана, мы услышали его крик.'

        stop music

        play sound 'interestingNumber/sound/gosha_screaming.mp3'

        cn 'Слышишь? Гошан кричит!'

        stop sound

        scene bg ext_no_bus with dissolve

        'Мы бегом добрались до Гошана. Тот сидел на корточках и держался за голову. Я подскочил к нему.'
        lx 'Гошан, что с тобой?'
        'Гошан аккуратно поднялся.'
        gh 'Чёрт. Отпустило. Хренотень какая-то, вы как отходить начали, вроде всё в порядке было.'
        gh 'А потом как начало бить по башке, как молотом по колоколу. Смотрю на вас - всё нормально, теряю вас из поля зрения - боль возвращается.'
        'Всех охватила паника. Совершенно непонятно, как объяснить произошедшее.'
        th 'Эй. Шиза, может, подскажешь?'
        iv '{i}Я не потусторонняя хрень. Я знаю то, что знаешь ты. Могу предположить, что это защита от хитрых придурков, которые решатся сбежать.{/i}'
        th 'Звучит правдоподобно.'
        'Я сделал пару вдохов и выдохов.'
        lx 'Пешком не получилось, значит, попробуем по-другому. Санёк, дай-ка карту.'
        'Саня, достав телефон, открыл на нём карту и показал изображение мне. Рассматривая карту, я обратил внимание на причал.'
        lx 'Парни, тут причал есть какой-то. Может, там лодки есть? Попробуем уплыть!'
        'Гошан немного взбодрился.'
        gh 'А что? Мысль. Можно попробовать.'
        'Саня никак не отреагировал и только кивнул. И мы пошли к лагерю...'

        scene bg ext_clubs_day with dissolve
        play ambience ambience_camp_center_day fadeout 1.5 fadein 1.5
        play music music_list['my_daily_life'] fadein 1.5

        'Идя снова рядом с клубами, мы приметили парнишку к кудрявой шевелюрой. Он нёс в руках ящик инструментов и, заметив нас положил его и начал махать нам рукой.'
        shp 'Эй. Вы же новенькие нашего отряда, верно?'
        'Наша группа остановилась.'
        lx 'Ты из нашего отряда?'
        'Парень кивнул.'
        th 'Слава Богу, что мы не одни парни.'
        cn 'Да, мы новенькие, а как тебя звать-то?'
        'Парень повеселел ещё сильнее.'
        shp 'Меня Серёгой звать. Многие меня Электроником кличут.'
        th 'Хм. А похож.'
        lx 'Рады знакомству. Это Саня и Гошан, а я Лёха.'
        'Серёга пожал нам руки.'
        el 'Я тоже очень рад. Слушайте, а не хотите в наш кружок кибернетики? Там есть я и Шурик, мы там такие крутые штуки делаем.'
        th 'Ммм. Пропаганда. Знаем, слышали.'
        'Саня уже хотел что-то сказать, но я его перебил.'

        if day1_attendMusicClub:
            jump AttendedMusicClubNoElectronic_day1
        else:
            jump NoElectronic_day1


        label NoElectronic_day1:
            scene bg ext_clubs_day
            lx 'Серёга, мы тут первый день, мало что видели. Дай осмотреться, а потом подумаем куда вступать.'
            jump AfterElectronic_day1


        label AttendedMusicClubNoElectronic_day1:
            scene bg ext_clubs_day
            lx 'Серый, дай парням осмотреться, а я уже записался в муз. клуб.'
            jump AfterElectronic_day1


        label AfterElectronic_day1:

            scene bg ext_clubs_day
            'Электроник немного погрустнел, но потом снова заулыбался.'
            el 'Да ладно. Я же не заставляю. Если захотите - приходите.'
            'Видимо он был очень рад своей рифмой и, взяв ящик, удалился в клубы.'
            lx 'Ладно. Идём дальше к причалу.'

            scene bg ext_boathouse_day with dissolve

            play ambience ambience_boat_station_day fadeout 1.5 fadein 2

            'Через пару минут мы вышли к лодочной станции. Как не странно, там были лодки, но без вёсел.'
            cn 'Отлично. Лодки есть, осталось найти вёсла.'
            'Мы начали осматривать весь причал. Тут мы услышали радостный крик Гошана.'
            gh 'Нашёл! Вёсла нашёл.'
            'Все подошли к лодкам.'
            lx 'Ну, кто грести будет?'
            'Я посмотрел на своих товарищей.'
            cn 'Пусть Гошан грести будет. Он же у нас деревенщина, сто пудов умеет это делать.'
            'Гошан недовольно глянул на Саню.'
            gh 'Ну, вообще-то, то, что я умею управляться с лодкой, не значит, что я деревенщина!'
            'Я похлопал его по плечу.'
            lx 'Да ладно тебе, Гошан. Ты из нас один мастер в этом деле.'
            'Гошан, бурча что-то под нос, полез в лодку.'
            'После подготовки лодки мы отдали концы и начали отплывать от причала. Тут Саня дёрнул меня за плечо.'
            cn 'Гляди. Это же железная дорога. И поезд там ездит. Может, попробуем на нём сбежать?'
            'Я устремил взгляд на железную дорогу.'
            lx 'А хорошая идея! Гошан, полный вперёд на тот берег!'
            'Гошан, кряхтя от недовольства, начал грести.'

            scene day1_railroad with dissolve
            play ambience ambience_camp_center_day fadeout 1.5 fadein 1.5

            'Через какое-то время мы стояли на берегу, где проходила железная дорога.'
            cn 'Пешком идти не выход, скорее всего, будет тоже самое, что на остановке.'
            'Я осмотрелся.'
            lx 'Значит, будем ждать поезд. Запрыгнем на него и...'
            'Меня прервал Саня.'
            cn 'И оторвём себе руки. Поезд не черепаха. Или ты думаешь, будет, как в фильмах?'
            'Я почесал затылок.'
            th 'Плохо, зря только приплыли.'
            iv '{i}Вы хотя бы узнали, что можете сюда приплыть.{/i}'
            th 'Ну, тоже верно. Ладно.'
            'Хлопнув в ладоши, я скомандовал.'
            lx 'Возвращаемся. Так как Гошан недееспособный, то грести я буду.'

            scene day1_lake with dissolve
            play ambience ambience_boat_station_day fadeout 1.5 fadein 1.5

            'Мы дружно залезли в лодку, а я сел за весла.'
            'На полпути гребли я понял, что это чертовски сложно. Руки начинали ныть, как и спина.'
            'Когда мы добрались до причала и зашли на него, я чувствовал себя, как выжатый лимон. Гошан разделял мои чувство, хотя он немного отдохнул. Один лишь Саня ни на что не жаловался.'
            'Немного отдохнув, мы отправились обратно в лагерь.'

            stop ambience fadeout 2

            scene bg ext_boathouse_day with dissolve

            'Идя в лагерь, в голову ничего не лезло из-за усталости. Нашу мирную ходьбу прервал Саня.'
            cn 'Слушайте, а может в лес? Может, так получится уйти?'
            'Мы взглянули вглубь леса.'
            lx 'Да ну. Чёрт знает, что там может быть. Так что, точно нет.'

            stop music fadeout 2

            cn 'Хм. Ну да, ты прав. Ладно, идём. Скоро, как я понял, ужин.'

            play sound sfx_dinner_jingle_normal

            'В этот момент прозвучал горн и мы поспешили к столовой.'

            stop sound

            scene bg ext_dining_hall_away_day with dissolve
            $ renpy.pause(1)
            scene bg int_dining_hall_people_day with dissolve
            play ambience ambience_dining_hall_full fadein 1
            play music '/interestingNumber/sound/k_stolovke.mp3' fadein 1.5

            play sound sfx_open_door_1

            'Нас встретил шум пионеров и звуки посуды. В столовой было полно людей. Из-за этого свободных стульев было мало.'

            stop sound

            'Наши глаза выцепили только два стола, где были свободны три стула.'

            if day1_wentToSquare:
                jump CafeteriaAfterSquare_day1
            if day1_wentToBusStop:
                jump CafeteriaAfterBusStop_day1
            if day1_wentToLibrary:
                jump CafeteriaAfterLibrary_day1


    label CafeteriaAfterSquare_day1:

        scene bg int_dining_hall_people_day

        if day1_wentWithSlavya:
            jump CafeteriaWentWithSlavya_day1
        else:
            jump CafeteriaWentWithoitSlavya_day1


        label CafeteriaWentWithSlavya_day1:

            scene bg int_dining_hall_people_day

            'За первым столом сидели неизвестные рыжие и девушка с аквамариновыми волосами.'
            'За вторым столом сидели Славя и также неизвестные для нас девушки.'

            menu:
                'Сесть за стол к Славе':
                    jump WentWithSlavyaSitWithSlavya_day1

                'Сесть за стол к неизвестным':
                    jump WentWithSlavyaSitWithOthers_day1


            label WentWithSlavyaSitWithSlavya_day1:

                scene bg int_dining_hall_people_day

                'Мы решили сесть со знакомым человеком. Взяв подносы с едой, мы отправились к столу, где сидела Славя.'
                lx 'Привет всем! Можно нашей компании к вам сесть?'
                'Девушки, сидящие за столом, посмотрели на нас. Теперь я лучше смог разглядеть не знакомых мне девушек.'
                'Первая была с синими волосами и смешным локоном на голове, также на ней были очки. Смотрела она на нас сердито, даже скорее раздраженно.'
                'Вторая была с фиолетовыми волосами. Она почти не смотрела на нас, так как была чем-то смущена. Я, даже на всякий случай проверил, застегнул ли ширинку.'
                'Из осмотра девушек меня вырвала Славя.'
                sl 'А, Лёша. Привет, привет. Конечно, садитесь. Мы будем рады.'
                'Мы дружно сели за стол и начали без осмотра есть то, что было на тарелках. Пока мы трапезничали, Славя решила заполнить пустоту, нависшую над нашим столом.'
                sl 'Ой. Мальчики, вы же, наверное, не знаете многих из нашего отряда.'
                'Мы, не отвлекаясь от еды, кивнули.'
                sl 'Тогда начнём с нашего стола. Вот это Лена и Женя.'
                'Она показала сначала на фиолетововолосую, а потом на девушку с очками.'
                lx 'Приятно познакомиться. Меня зовут Лёша, а это Саня и Гоша.'
                'Саня и Гошан лишь угукнули. Женя, поправив очки, сказала.'
                mz 'Да. Приятно.'
                'Из её слов так и тёк сарказм, что хоть ведрами собирай. Лена просто тихо кивнула. Славя продолжила.'
                sl 'Также, за тем столиком сидят: Алиса, та, которая рыжая старшая, Ульяна, та, которая маленькая, и Мику, девушка с аквамариновыми волосами.'
                'Назвав их имена, она вкратце рассказала кто чем занимается.'
                th 'Полезная информация.'

                if slPoints == 0:
                    jump SlavyaExtendedDialog_day1

                'Доедали мы в полной тишине. Поев и попрощавшись с девушками, мы сдали посуду, и вышли из столовой.'

                jump AfterCafeteria_day1


            label WentWithSlavyaSitWithOthers_day1:

                scene bg int_dining_hall_people_day

                'Мы решили сесть за первый стол. Взяв подносы с едой мы отправились к столу рыжих.'
                lx 'Привет всем! Можно нашей компании к вам сесть?'
                'Рыжие повернули на нас голову, а вот аквамариновая даже не заметила, что мы подошли, и продолжала о чём-то говорить. Только сейчас я смог их нормально рассмотреть.'
                'Рыжая старшая была с медно рыжими волосами, на голове были небольшие хвостики, а на лице красовалась ухмылка. Вторая вещь, выдающая в ней бунтарку - это её форма. Галстук на руке, открытый живот и завязанный бант ниже груди.'
                'Мелкая рыжая была стандартным ребёнком - ракетой. Даже её прическа говорила об этом. Ярко красные волосы, заплетённые, как два сопла ракеты. Она смотрела на нас, будто строя план, как бы над нами пошутить.'
                'Девушка, которая продолжала говорить, имела длинные аквамариновые волосы. По росту она была средней в компании. Также, я заметил азиатские черты на её лице. Говорила она с закрытыми глазами и приятной улыбкой.'
                'Из осмотра девушек меня вырвала рыжая старшая.'
                dvp 'Новенькие?'
                'Мы кивнули. Азиатка вдруг остановилась.'
                mip 'Новенькие? Где?'
                'Она открыла глаза и устремила свой взор на нас. Тут подала голос мелкая.'
                usp 'Да вот они. Товарищи опоздавшие.'
                'Я решил повторить свой вопрос.'
                lx 'Так мы можем сесть?'
                'Рыжая старшая начала сверлить меня взглядом и через несколько секунд ответила.'
                dvp 'Садитесь, конечно. Почему нет?'
                'Мы сели за стол и начали есть.'
                'Я следил за мелкой, так как моё шестое чувство подсказывало, что от неё можно ждать неприятностей.'
                'Над столом нависла тишина, и я решил её заполнить.'
                lx 'Слушайте, мы же не знакомы...'
                'Не успел я договорить, как азиатка, до этого момента ничего не говорившая, дала очередь из словесного пулемета.'
                mip 'Ой, да, точно! Я вот Мику, честно. У меня мать японка, а отец...'
                'Дальше я больше не слушал, и, изредка кивая, продолжал есть.'
                'Когда она закончила, я сказал.'
                lx 'Приятно познакомиться. А вы, рыжие дамы?'
                'Я посмотрел на двух рыжих.'
                dvp 'Меня Алиса, а вот эту мелкую - Ульяна.'
                us 'Угу.'
                'Ульяна уже не смотрела в нашу сторону, а просто игралась с едой.'
                lx 'Рад знакомству. Меня можете называть Лёхой, а это Саня и Гошан.'
                'Саня и Гоша только кивнули.'
                dv 'Рада познакомится.'
                'Тут снова ворвалась Мику.'
                mi 'Лёша, а вы знакомы с другим столиком? Там тоже наши товарищи по отряду. Славя, Лена и Женя. Женя главная в библиотеке, а Лена иногда ей помогает, а Славя помощница вожатой. Вот.'
                th 'Полезная информация.'
                lx 'Спасибо за информацию.'
                'После этого все за столиком, кроме Мику, молча доедали, а она без умолку о чём-то говорила.'
                'Поев и попрощавшись с девушками, мы сдали посуду, и вышли из столовой.'

                jump AfterCafeteria_day1


        label CafeteriaWentWithoitSlavya_day1:

            scene bg int_dining_hall_people_day
            'За первым столом сидели две рыжие, одна из них была нам уже знакома. Рядом с ней была мелкая девочка и девушка с аквамариновыми волосами.'
            'За вторым столом сидели Славя и неизвестные для нас девушки.'

            menu:
                'Сесть за стол к Славе':
                    jump WentWithSlavyaSitWithSlavya_day1

                'Сесть за стол к рыжим':
                    if day1_helpedGosha:
                        jump HelpedGoshaSitWithOthers_day1
                    else:
                        jump NotHelpedGoshaSitWithOthers_day1


            label HelpedGoshaSitWithOthers_day1:

                scene bg int_dining_hall_people_day

                'Мы решили сесть за стол к уже знакомой нам рыжей.'
                lx 'Привет, с кем ещё не виделся. К вам можно?'
                'Все, кто сидел за столом, повернули головы на нас, кроме аквамариновой девушки, она продолжала о чем-то говорить. Если рыжую я уже знал, то других участников стола видел впервые.'
                'Мелкая рыжая была стандартным ребёнком - ракетой. Даже её прическа говорила об этом. Ярко красные волосы, заплетённые, как два сопла ракеты. Она смотрела на нас, будто строя план, как бы над нами пошутить.'
                'Девушка, которая продолжала говорить, имела длинные аквамариновые волосы. По росту она была средней в компании. Также, я заметил азиатские черты на её лице. Говорила она с закрытыми глазами и приятной улыбкой.'
                'Из осмотра девушек меня вырвала Рыжая старшая.'
                dvp 'А, привет снова.'
                'Я лишь кивнул.'
                usp 'А вы те новенькие, да?'
                'Мы кивнули. Азиатка вдруг остановилась.'
                mip 'Новенькие? Где?'
                'Она открыла глаза и устремила свой взор на нас.'
                'Я решил повторить свой вопрос.'
                lx 'Так можно сесть?'
                'Рыжая старшая сделал задумчивый вид, но через секунду ответила.'
                dvp 'Так уж и быть. Садитесь.'
                'Она сделал ехидное лицо. Саня решил сострить.'
                cn 'Спасибо, ваше хрен знает, как зовут высочество.'
                'И сел за стол, мы последовали его примеру.'
                'Сначала Рыжая начала злиться, но потом, видимо, поняла эту остроту и лишь кивнула головой. Мы приступили к трапезе.'
                'Я поднял взгляд на Рыжую Старшую. У неё был задумчивый взгляд.'
                'Через секунду она выдала.'
                dvp 'Слушайте, мы же не знаем, как вас зовут, да и вы не знаете наших имён.'
                'Парни отвлеклись от еды. Не успел я открыть рот, как азиатка пустила очередь из словесного пулемета.'
                mip 'Ой, да, точно! Я вот Мику, честно. У меня мать японка, а отец...'
                'Дальше я больше не слушал, и, изредка кивая, продолжал есть.'
                'Когда она закончила, я сказал.'
                lx 'Приятно познакомиться. А вы, рыжие дамы?'
                'Я посмотрел на двух рыжих.'
                dvp 'Меня Алиса, а вот эту мелкую - Ульяна.'
                'Мелкая пристально смотрела на меня.'
                us 'Правда, что ты Алиску не побоялся и за друга заступился?'
                'Я не ожидал такой вопрос, а Гошан аж поперхнулся и покраснел.'
                lx 'Ну да. На то они друзья.'
                'Ульяна на это ничего не ответила.'
                dv 'Да, правильно поступил.'
                'Алиса улыбнулась и продолжила есть.'
                th 'Хм. Уважение от двоих, как я понял, бунтарок сие лагеря. Неплохо.'
                'Я вспомнил, что мы ещё не представились.'
                lx 'Кстати, меня можете называть Лёхой, а это Саня и Гошан.'
                'Саня и Гоша только кивнули.'
                dv 'Рада знакомству.'
                'Ульяна также, как и парни, просто кивнула. Тут снова ворвалась Мику.'
                mi 'Лёша, а вы знакомы с другим столиком? Там тоже наши соотрядцы. Славя, Лена и Женя. Женя главная в библиотеке, а Лена иногда ей помогает, а Славя помощница вожатой. Вот.'
                'Полезная информация.'
                lx 'Спасибо за информацию.'
                'После этого все за столиком, кроме Мику, молча доедали, а она без умолку о чём-то говорила.'
                'Поев и попрощавшись с девушками, мы сдали посуду, и вышли из столовой.'

                jump AfterCafeteria_day1


            label NotHelpedGoshaSitWithOthers_day1:

                scene bg int_dining_hall_people_day

                'Мы решили сесть за стол к рыжим. По лицу Гошана было видно, что он был недоволен таким решением.'
                lx 'Кхм. Можно к вам?'
                'Все, кто сидел за столом, повернули головы на нас, кроме аквамариновой девушки, она продолжала о чем-то говорить. Только сейчас я смог их нормально рассмотреть.'
                'Хоть я и видел рыжую старшую, но вблизи ёе было видно лучше. У неё были медно-рыжие волосы, завязанные небольшие хвостики, а на лице красовалась ухмылка.'
                'Ещё одна вещь, выдающая в ней бунтарку - это её форма. Галстук на руке, открытый живот и завязанный бант ниже груди.'
                'Рыжая мелкая была стандартным ребенком - ракетой. Даже её прическа говорила об этом.'
                'Ярко красные волосы, заплетённые как два сопла ракеты. Она смотрела на нас, будто строя план, как бы над нами пошутить.'
                'Девушка, которая продолжала говорить, имела длинные аквамариновые волосы. По росту она была средней в компании. Также, я заметил азиатские черты на её лице. Говорила она с закрытыми глазами и приятной улыбкой.'
                'Из осмотра девушек меня вырвала рыжая старшая.'
                dvp 'А, новенькие. А этот молчун с вами, что ли?'
                'Она кивнула на Гошана. Тот покраснел и снова уставился в пол.'
                lx 'Да, да, с нами, его Гошан зовут, а меня - Лёха.'
                'Азиатка вдруг перестала говорить.'
                mip 'Новенькие? Где?'
                'Она открыла глаза и устремила свой взор на нас. Я перевел взгляд снова на рыжую старшую.'
                lx 'Кстати, если будут вопросы, подходи ко мне или к Сане.'
                'Саня поднял руку в знак приветствия.'
                lx 'Хорошо?'
                'Рыжая ничего не ответила. Я решил повторить свой вопрос.'
                lx 'Сесть-то можно?'
                'Она снова посмотрела на меня.'
                dvp 'Хм. Ну садитесь.'
                'Мы дружно сели.'
                dvp 'Я, кстати, Алиса, а это Ульяна.'
                us 'Угу.'
                'Она даже не смотрела в нашу сторону, а просто игралась с едой.'
                'Я посмотрел на азиатку.'
                lx 'А тебя...'
                'Не успел я договорить, как в меня пустили очередь из слов.'
                mip 'Ой, да! Точно! Я вот Мику, честно. У меня мать японка, а отец...'
                'Дальше я не слушал, и, изредка кивая, продолжал есть.'
                'Когда она закончила, я сказал:'
                lx 'Приятно познакомиться.'
                'Тут снова ворвалась Мику.'
                mi 'Лёша, а вы знакомы с другим столиком? Там тоже наши соотрядцы. Славя, Лена и Женя. Женя главная в библиотеке, а Лена иногда ей помогает, а Славя помощница вожатой. Вот.'
                'Полезная информация.'
                lx 'Спасибо за информацию.'
                'После этого все за столиком, кроме Мику, молча доедали, а она без умолку о чём-то говорила.'
                'Поев и попрощавшись с девушками, мы сдали посуду, и вышли из столовой.'

                jump AfterCafeteria_day1


            label SlavyaExtendedDialog_day1:

                scene bg int_dining_hall_people_day

                'Я уже хотел вернуться к еде, но Славя не дала мне этого сделать.'
                sl 'Лёша, а вы смогли что-нибудь посмотреть в нашем лагере? Я хотела предложить помощь, но вы так быстро ушли...'
                lx 'Ну, мы не много увидели.'
                th 'Ага. Ничего.'
                lx 'Но у нас ещё много времени, всё увидем.'
                'Славя улыбнулась.'
                sl 'Ну хорошо, если что, обращайтесь, всегда подскажу.'
                'Я лишь кивнул в ответ.'
                'Доедали мы в полной тишине. Поев и попрощавшись с девушками, мы сдали посуду, и вышли из столовой.'

                jump AfterCafeteria_day1


    label CafeteriaAfterBusStop_day1:

        scene bg int_dining_hall_people_day

        'За первым столом сидели две рыжие и Мику, которая о чём-то оживлённо рассказывала.'
        'За вторым столом сидели незнакомые для нас девушки.'

        menu:
            'Сесть к Мику':
                jump WentToBusStopSitWithMiku_day1

            'Сесть за стол к незнакомым':
                jump WentToBusStopSitWithOthers_day1


        label WentToBusStopSitWithMiku_day1:

            scene bg int_dining_hall_people_day

            'Мы решили сесть к Мику. Взяв подносы с едой, мы отправились к её столу.'
            'Я уже открыл рот, но наша новая подружка меня перебила.'
            mi 'Ой, Лёша, Саша и Гоша. Вы к нам? Садитесь, конечно. А вы же не знакомы с моими подругами, верно?'
            'Я было хотел ответить, но Мику вновь меня перебила.'
            mi 'Конечно не знакомы. Это Алиса.'
            'Она показала на рыжую девушку постарше.'
            mi 'А это Ульяна.'
            'Она показала на мелкую, сидящую рядом с первой.'
            'Когда Мику закончила, я всё-таки смог сказать.'
            lx 'Рад знакомству. Как вы поняли, я - Лёха, а это Саня и Гошан. Вы не против, если мы сядем с вами?'
            'Алиса осмотрела меня и моих друзей.'
            dv 'Рада знакомству. Садитесь, конечно.'
            'Мы дружно сели за стол.'
            'Только сейчас я смог нормально рассмотреть рыжих.'
            'Алиса была с медно-рыжими волосами, на голове были небольшие хвостики, а на лице красовалась ухмылка.'
            'Одна из вещей, выдающих в ней бунтарку - это её форма. Галстук на руке, открытый живот и завязанный ниже груди бант из рубашки.'
            'Ульяна была стандартным ребенком - ракетой. Даже её прическа говорила об этом.'
            'Ярко красные волосы, заплетённые как два сопла ракеты. Она смотрела на нас, будто строя план, как бы над нами пошутить.'
            'После осмотра новых лиц, я приступил к трапезе.'
            'По видимому, не выдержав тишины, Мику снова начала говорить.'

            if day1_attendMusicClub:
                jump AttendedMusicClubMikuExtendedDialog_day1
            else:
                jump NotAttendedMusicClubMikuExtendedDialog_day1


            label AttendedMusicClubMikuExtendedDialog_day1:

                scene bg int_dining_hall_people_day

                mi 'Алиса, я забыла тебе сказать. Лёшенька записался в муз. клуб.'
                th 'Брр. Вымораживает, когда говорят \"Лёшенька\", меня так только бабка родная называла.'
                iv '{i}Да ладно, по-моему, звучит мило. Лёшенька. Теперь буду тебя так всегда называть.{/i}'
                th 'Вернусь, точно к врачу схожу.'
                iv '{i}Гыгы.{/i}'
                'Мику продолжила говорить.'
                mi 'Он так хорошо играет на гитаре, правда, есть небольшие недочеты, но их исправить можно.'
                'Алиса оторвалась от еды и посмотрела сначала на Мику, а потом на меня.'
                dv 'Хорошо играешь, да? Надо как-нибудь проверить.'

                jump MikuDialogContinue_day1


            label NotAttendedMusicClubMikuExtendedDialog_day1:

                scene bg int_dining_hall_people_day

                mi 'Лёшенька, может, всё-таки запишешься в клуб? Ты так хорошо играешь на гитаре, немного исправить недочёты и всё.'
                'Я оторвался от еды.'
                lx 'Мику, я же говорил. Я тут только первый день, а хочется всё осмотреть.'
                'Тут Алиса встряла в наш разговор.'
                dv 'Хорошо умеешь играть на гитаре? Что-то мне не особо верится, надо будет проверить твои способности.'

                jump MikuDialogContinue_day1


            label MikuDialogContinue_day1:

                scene bg int_dining_hall_people_day

                lx 'В любое время, в любую погоду.'

                'Я улыбнулся. Алиса лишь хмыкнула и вернулась к еде, я поступил также.'
                'Но тут снова ворвалась Мику.'
                mi 'Лёша, а вы знакомы с другим столиком? Там тоже наши соотрядцы. Славя, Лена и Женя. Женя главная в библиотеке, а Лена иногда ей помогает, а Славя помощница вожатой. Вот.'
                th 'Полезная информация.'
                lx 'Спасибо большое, что рассказала. Познакомимся с ними как-нибудь позже.'
                'Доедали мы в полной тишине. Поев и попрощавшись с девушками, мы сдали посуду, и вышли из столовой.'

                jump AfterCafeteria_day1


        label WentToBusStopSitWithOthers_day1:

            scene bg int_dining_hall_people_day

            'Мы решили сесть за второй стол. Взяв подносы с едой, мы отправились к столу, где сидели неизвестные нам девушки.'
            lx 'Привет всем! Можно к вам сесть?'
            'Девушки, сидящие за столом, посмотрели на нас. Теперь я лучше смог разглядеть незнакомок.'
            'Первая была с синими волосами и смешным локоном на голове, также на ней были очки. Смотрела она на нас сердито, даже скорее раздражённо.'
            'Вторая была с фиолетовыми волосами. Она почти не смотрела на нас, так как была чем-то смущена. Я даже на всякий случай проверил, застегнул ли ширинку.'
            'Третья девушка была с золотыми волосами, заплетёнными в косу, а глаза были цвета неба. Она единственная из всех сидящих дружелюбно улыбалась.'
            'Эта девушка и заговорила первой.'
            slp 'Вы, наверное, Лёша, Гоша и Саша, верно?'
            'Мы недоуменно кивнули. Девушка поняла наше замешательство.'
            slp 'Я помощница вожатой, поэтому и знаю ваши имена. Меня зовут Славя. А это Женя и Лена.'
            'Она показала сначала на синеволосую, потом на ту, что с фиолетовыми волосами.'
            lx 'Рады знакомству. Как вы поняли, я - Лёха, а это Гоша и Саня.'
            'Девушки ничего не ответили, лишь кивнули. А Славя продолжила.'
            sl 'Также, за тем столиком сидят: Алиса, та, которая рыжая постарше, Ульяна, которая маленькая, и Мику - девушка с аквамариновыми волосами.'
            'Назвав их имена, она вкратце рассказала кто чем занимается.'
            th 'Ну, не считая Мику, про остальных информация полезная.'
            'Но я решил повторить вопрос.'
            lx 'А сесть-то можно?'
            sl 'Конечно, садитесь.'
            'Мы дружно сели и приступили к трапезе.'
            'Через какое-то время Славя прервала тишину над нашим столом.'
            sl 'Кстати, Лёша, вы же знаете, в какой отряд попали?'
            'Я отрицательно помахал головой. Тут из ниоткуда появился Саня.'
            cn 'Я надеюсь, что с такими красивыми девушками, как вы?'
            'Гошан поперхнулся, а я просто замер и наблюдал за реакцией девушек.'
            th 'Надеюсь, что одна из них вывалит ему еду на голову. Да что с ним не так?'
            'Девушки отреагировали по-разному: Лена покраснела ещё сильнее, Женя даже в лице не переменилась, а Славя, на моё удивление, смутилась.'
            'Когда девушки отошли от \"великолепного\" комплимента Сани, Славя продолжила.'
            sl 'Вы, мальчики, в нашем отряде. В первом.'
            th 'И почему Ольга нам об этом не говорила?'
            lx 'Спасибо за информацию, будем знать.'
            'Доедали мы в полной тишине. Поев и попрощавшись с девушками, мы сдали посуду, и вышли из столовой.'

            jump AfterCafeteria_day1


    label CafeteriaAfterLibrary_day1:

        scene bg int_dining_hall_people_day

        if day1_helpedLena:
            jump CafeteriaHelpedLenaInLibrary_day1
        else:
            jump CafeteriaNotHelpedLenaInLibrary_day1


        label CafeteriaHelpedLenaInLibrary_day1:

            scene bg int_dining_hall_people_day

            'За первым столом сидели неизвестные рыжие и девушка с аквамариновыми волосами.'
            'За вторым столом сидели Женя, Лена и незнакомая нам светловолосая девушка.'

            menu:
                'Сесть за стол к Жене и Лене':
                    jump WentToLibrarySitWithLena_day1
                'Сесть к неизвестным':
                    jump WentWithSlavyaSitWithOthers_day1   # Диалог тот же самый, поэтому используется фрагмент от Слави


            label WentToLibrarySitWithLena_day1:

                scene bg int_dining_hall_people_day

                'Немного подумав, мы решили сесть к знакомым людям. Взяв подносы с едой, мы отправились к Лене и Жене.'
                lx 'Привет тем, кого сегодня не видели! Можно нашей компании к вам сесть?'
                'Девушки, сидящие за столом, посмотрели на нас.'
                'Теперь я смог рассмотреть светловолосую получше.'
                'Её волосы отливали золотом и были заплетены в косу. Глаза цвета неба очень подходили её образу. Она единственная из всех сидящих дружелюбно улыбалась.'
                'Первой заговорила Женя.'
                mz 'А, это вы. Садитесь. Кстати, спасибо за помощь.'
                th 'Вау, она умеет благодарить? Вот это новость.'
                lx 'Да не за что.'
                'Лена лишь кивнула. Я уже хотел спросить имя светловолосой, но она опередила меня.'

                jump SlavyaKnowsEveryone_day1


            label SlavyaKnowsEveryone_day1:

                scene bg int_dining_hall_people_day

                slp 'Вы, наверное, Лёша, Гоша и Саша, верно?'
                'Мы недоуменно кивнули. Девушка поняла наше замешательство.'
                slp 'Я помощница вожатой. Поэтому и знаю ваши имена. Меня зовут Славя.'
                lx 'Рады знакомству.'
                sl 'Как я поняла, с девушками за столом вы знакомы. А других товарищей по отряду знаете?'
                'Я отрицательно покачал головой.'
                sl 'Вот за тем столиком сидят: Алиса, та, которая рыжая постарше, Ульяна, которая маленькая, и Мику - девушка с аквамариновыми волосами.'
                'Назвав их имена, она вкратце рассказала кто чем занимается.'
                th 'Полезная информация.'
                lx 'Спасибо.'
                'После этого мы дружно сели и приступили к трапезе.'
                'Через какое-то время Славя прервала тишину над нашем столом.'
                sl 'Кстати, Лёша, вы же знаете, в какой отряд попали?'

                if day1_helpedLena:
                    jump SashaJoking_day1
                else:
                    jump SashaQuite_day1


            label SashaJoking_day1:

                scene bg int_dining_hall_people_day

                'Я отрицательно помахал головой. Тут из ниоткуда появился Саня.'
                cn 'Я надеюсь, что с такими красивыми девушками, как вы?'
                'Гошан поперхнулся, а я просто замер и наблюдал за реакцией девушек.'
                th 'Надеюсь, что одна из них вывалит ему еду на голову. Да что с ним не так?'
                'Девушки отреагировали по-разному: Лена покраснела ещё сильнее, Женя даже в лице не переменилась, а Славя, на моё удивление, смутилась.'
                'Когда девушки отошли от \"великолепного\" комплимента Сани, Славя продолжила.'
                sl 'Вы, мальчики, в нашем отряде. В первом.'
                th 'И почему Ольга нам об этом не говорила?'
                lx 'Спасибо за информацию, будем знать.'
                'Доедали мы в полной тишине. Поев и попрощавшись с девушками, мы сдали посуду, и вышли из столовой.'

                jump AfterCafeteria_day1


        label CafeteriaNotHelpedLenaInLibrary_day1:

            scene bg int_dining_hall_people_day

            'За первым столом сидела Ульяна и ещё какая-то рыжая, также рядом с ними была девушка с аквамариновыми волосами.'
            'За вторым столом сидели Женя и Лена, которым мы отказали в помощи. Также там была незнакомая нам светловолосая девушка.'

            menu:
                'Сесть к Жене и Лене':
                    jump NotHelpedLenaInLibrarySitWithLena_day1

                'Сесть к Ульяне':
                    if day1_agreedWithUlyana:
                        jump NotHelpedLenaInLibraryAgreedWithUlyanaSitWithUlyana_day1
                    else:
                        jump NotHelpedLenaInLibraryNotAgreedWithUlyanaSitWithUlyana_day1


            label NotHelpedLenaInLibrarySitWithLena_day1:

                scene bg int_dining_hall_people_day

                'Всё-таки мы решили сесть к Жене и Лене, заодно можно будет попросить прощения за то, что не помогли.'
                'Взяв подносы с едой, мы отправились к знакомым девушкам.'
                lx 'Привет тем, кого сегодня не видели! Можно нашей компании к вам сесть?'
                'Девушки, сидящие за столом, посмотрели на нас. Теперь я смог рассмотреть светловолосую получше.'
                'Её волосы отливали золотом и были заплетены в косу. Глаза цвета неба очень подходили её образу. Она единственная из всех сидящих дружелюбно улыбалась.'
                'Первой заговорила Женя.'
                mz 'А, это же компания \"простите у нас ещё дела\". Ясно, как помочь с уборкой, так у нас дела. А как поесть, так всегда пожалуйста.'
                'Она сверлила нас злобным взглядом.'
                lx 'Ну, эээ... У нас и правда были дела. К тому же, мы же извинились.'
                'Женя лишь фыркнула и вернулась к тарелке.'
                'Тут заговорила светловолосая.'
                slp 'Не беспокойтесь так, потом сможете отмыть свою репутацию.'
                'Она тепло улыбнулась.'
                slp 'А пока садитесь за стол. Меня, кстати, зовут Славя.'
                'Мы сели за стол.'

                jump SlavyaKnowsEveryone_day1


            label SashaQuite_day1:

                scene bg int_dining_hall_people_day

                'Краем глаза я увидел, что Саня уже хотел что-то сказать, но потом посмотрел на Женю и решил промолчать.'
                th 'Интересно, что он хотел сказать? Ладно, неважно, надо послушать Славю.'
                sl 'Вы в первом отряде, в нашем то есть.'
                'Я почесал затылок.'
                lx 'То есть, вы тоже в первом?'
                iv '{i}Во тугодум.{/i}'
                'Девушки кивнули.'
                lx 'Ясно.'
                'Доедали мы в полной тишине. Поев и попрощавшись с девушками, мы сдали посуду, и вышли из столовой.'

                jump AfterCafeteria_day1


            label NotHelpedLenaInLibraryAgreedWithUlyanaSitWithUlyana_day1:

                'Наш выбор пал на стол мелкой ракеты. Лучше уж к ней, чем чувствовать себя неловко за то, что не помогли с уборкой Жене и Лене.'
                'Взяв подносы с едой, мы отправились к мелкой.'
                lx 'Всем хорошего дня. О, мелкая, ну как делишки?'
                'Ульяна сначала не поняла, кто к ней обратился, но, увидев нас, заулыбалась.'
                us 'О, парни, привет. Вам сесть негде? Так садитесь тут, будем рады.'
                'Тут заговорил Саня.'
                cn 'Ракета, мы бы не против сесть, но вот хотят ли другие участники этого стола, чтоб наша компания тут приземлилась?'
                'Ульяна умоляюще посмотрела на рыжую соседку, которая до этого момент просто нас разглядывала. От такого взгляда мелкой она улыбнулась.'
                dvp 'Если Уля просит, значит можно.'
                'Мы дружно сели за стол. Аквамариновая девушка так и не заметила нас и продолжала о ч1м-то говорить.'
                us 'С нашим столом вы познакомитесь, а вот за соседним столиком знаете кто сидит?'
                lx 'Знаем всех, кроме той с косой.'
                us 'Это Славя, помощница вожатой. Большего вам не надо.'
                th 'Ну, хоть имя и род деятельности знаю.'
                lx 'Спасибо за информацию.'
                'После этого я всё-таки решил рассмотреть двух других девушек.'

                jump AdmiringGirls_day1


            label AdmiringGirls_day1:

                scene bg int_dining_hall_people_day

                'Рыжая, что постарше, была с медно-рыжими волосами, заплетёнными небольшие хвостики, а на лице красовалась ухмылка. Вещь, выдающая в ней бунтарку - это её форма.'
                'Галстук на руке, открытый живот и рубашка, завязанная в бант ниже груди.'
                'Девушка, которая продолжала говорить, имела длинные аквамариновые волосы. Она была ниже бунтарки, но выше Ульяны. Также, я заметил азиатские черты на её лице. Говорила она с закрытыми глазами и приятной улыбкой.'
                'Из осмотра девушек меня вырвала рыжая.'

                if day1_agreedWithUlyana:
                    jump AgreedWithUlyanaDialogContinue_day1
                else:
                    jump NotAgreedWithUlyanaDialogContinue_day1


                label AgreedWithUlyanaDialogContinue_day1:

                    scene bg int_dining_hall_people_day

                    dvp 'Вы же новенькие, верно?'
                    'Мы кивнули. Азиатка вдруг остановилась.'
                    mip 'Новенькие? Где?'
                    'Она открыла глаза и устремила свой взор на нас.'
                    lx 'Слушайте, мы же не знакомы с вами, не считая мелкую.'
                    'После этих слов аквамаринчик встрепенулась.'
                    mip 'Ой, да, точно! Я вот Мику, честно. У меня мать японка, а отец...'
                    'Она хотела продолжить, но её прервала Ульяна.'
                    us 'Мику, остановись, а то у них голова взорвётся от твоей словесной пулемётной очереди.'
                    'Мику нахмурилась.'
                    mi 'И ничего не пулемёт. Да ведь, мальчики?'
                    'Я и Гошан просто кивнули, не желая встревать в конфликт, но Саня решил выделится.'
                    cn 'Верно, я не вижу пулемёта. Просто Мику очень общительная, вот и всё.'
                    'Мику вернула себе прежнее лицо.'
                    us 'Ой, ну вот и сиди с Мику отдельно, посмотрим, через сколько минут у тебя мозг в кашу сварится.'
                    th 'Началось. И зачем Саня в это полез? Скучно жить что ли?'
                    cn 'А ты завидуешь, Ракета?'
                    'Ульяна начала злиться.'
                    us 'Я? Завидую? Чему же, интересно?!'
                    'Саня понял, что он смог вывести на реакцию своего оппонента и улыбался.'
                    cn 'Откуда же мне знать?'
                    'Ульяна не знала, что сказать.'
                    'Поняв, что он уже победил, мы перевели взгляд на её соседку, которая так и не представилась.'
                    cn 'Кстати, а как тебя-то величать?'
                    'Она оторвалась от еды и посмотрела на Саню.'
                    dvp 'Алиса. Рада знакомству.'
                    'Сказав это, она вернулась к еде.'
                    th 'Хм. Алиса. Что-то она на Селезнёву не смахивает.'
                    iv '{i}Ого, вот это ты вспомнил. Эх, как думаешь, где ты профукал своё прекрасное далёко?{/i}'
                    th 'Да ну тебя. У тебя там соль бесконечная что ли?'
                    iv '{i}Ваш шеф-повар всегда рядом. Гыгыгы.{/i}'
                    'Доедали мы в полной тишине, даже Мику. Видимо, её всё-таки задела фраза Ульяны и она решила помолчать.'
                    'Поев и попрощавшись с девушками, мы сдали посуду и вышли из столовой.'

                    jump AfterCafeteria_day1


            label NotHelpedLenaInLibraryNotAgreedWithUlyanaSitWithUlyana_day1:

                scene bg int_dining_hall_people_day

                'Наш выбор пал на стол мелкой ракеты. Хотя мне не понравился её прикол над теми двумя девушками, но лучше уж к ней, чем чувствовать себя неловко за то, что не помогли им с уборкой.'
                'Взяв подносы с едой, мы отправились к мелкой.'
                lx 'Приятного всем. К вам можно?'
                'Ульяна посмотрела на нас и улыбнулась.'
                us 'Привет Саня, Гошан. Ну и тебе, правильный.'
                th 'Класс. Она теперь всегда так будет меня называть?'
                'Саня улыбнулся.'
                cn 'Здарова, мелкая. К вам можно?'
                'Ульяна осмотрела своих подруг.'
                us 'Конечно можно. Давайте я вас познакомлю с нашим столом.'
                'Мы одобрительно кивнули и сели на свободные стулья.'
                us 'Это, парни. С нашим столом вы познакомитесь, а вот за соседним столиком знаете кто сидит?'
                'Я предоставил диалог Сане, так как Ульяна, видимо, не хочет со мной говорить.'
                cn 'Ну, ракета, рассказывай. Знаем всех, кроме той с косой.'
                us 'Это Славя, помощница вожатой. Большего вам знать и не надо.'
                th 'Ну, хоть имя и род деятельности знаю.'
                cn 'Мда, мелкая. Ну и на этом спасибо.'
                'Пока мелкая не приступила к знакомству с нашим столом, я решил рассмотреть двух других девушек.'

                jump AdmiringGirls_day1


                label NotAgreedWithUlyanaDialogContinue_day1:

                    scene bg int_dining_hall_people_day

                    'Из осмотра девушек меня вырвала ракета.'
                    us 'Вот это Алиса.'
                    'Она показала на рыжую соседку. Та, в свою очередь, подняла голову на нас.'
                    dv 'Угу. Это о вас рассказывала Ульяна? Полу-разведчик, пухляш и правильный.'
                    th 'Да вы издеваетесь, эта рыжая тоже так будет меня называть?!'
                    'Саня был доволен своим описание, а Гошану было наплевать, так как такое он уже привык слышать.'
                    lx 'Я - Лёха. Это Саня и Гошан. Рады знакомству.'
                    'Я решил, чем поскорее назову своё имя, тем быстрее они забудут меня как \"правильного\".'
                    'Алиса лишь кивнула и вернулась к еде. Ульяна продолжила знакомить нас со своими подругами.'
                    us 'Вот это Мику. Девушка-оркестр. Ну, ещё она очень много говорит, да что там, она и сейчас не может остановиться.'
                    'Мику и вправду до сих пор говорила, не замечая нас. Пришлось привлечь её внимание.'
                    lx 'Кхм. Мику, верно?'
                    'Мику остановилась, открыла глаза и посмотрела на нас.'
                    mi 'Ой. А я и не заметила, что вы к нам сели. Мы разве знакомы? Откуда ты моё имя знаешь?'
                    'Она недоумённо глядела на нас.'
                    lx 'Ну, нам Ульяна рассказала. Мы новенькие. Я - Лёха, а это Саня и Гошан. Рады знакомству.'
                    'После небольшого осмотра нас троих, она вернулась к той же улыбке, что была до этого.'
                    mi 'А я как рада! Новые лица - это всегда хорошо. Как вы поняли, меня зовут Мику. Честно, честно. У меня мама японка, а папа...'
                    'Дальше я уже не слушал и, изредка кивая, продолжал есть. В оставшееся время, не считая Мику, никто ничего не сказал.'
                    'Поев и попрощавшись с девушками, мы сдали посуду и вышли из столовой.'

                    jump AfterCafeteria_day1

    label AfterCafeteria_day1:

        scene bg ext_dining_hall_near_sunset with dissolve
        $ persistent.sprite_time = 'sunset'
        $ sunset_time()
        play ambience ambience_camp_center_evening fadeout 1.5 fadein 1.5
        stop music fadeout 2
        play sound sfx_open_door_1

        'На улице уже вечерело.'

        stop sound

        cn 'Мы поели, спать ещё рано, что делать будем?'
        'Гошан пожал плечами. Я тоже не знал что делать.'
        th 'За сегодня мы нихрена не узнали. Ни дат, ни где мы.'
        th 'Единственное, что знаем - это что нас запихнули, вроде бы, в наш СССР, в пионерлагерь, и то, что отсюда, на основе наших экспериментов, не выбраться. Класс.'
        'Но вдруг, мой блуждающий взгляд выцепил развалюху, которая когда-то была машиной.'

        play music '/interestingNumber/sound/let_see_car.mp3' fadein 1.5

        lx 'Парни, видете этот конструктор нашего автопрома? Давайте-ка посмотрим, работает ли она.'
        'Подождав, когда все пионеры уйдут, наша компашка двинула на осмотр авто.'

        stop ambience fadeout 1.5

        'Гошан и Саня встали на стрём, а я полез в машину.'
        'Стекла у двери не было и я с лёгкостью мог проникнуть в машину, но меня интересовало другое. {w}Поискав кнопку открытия капота и нажав на нее, я двинул к двигателю.'
        iv '{i}И что ты тут ищешь? Понятно, что если этот автомобиль премиум класс тут оставили, то он не работает.{/i}'
        th 'Я и без тебя знаю, но вдруг починить можно?'
        iv '{i}Ты думаешь, что сможешь уехать? Вряд ли.{/i}'
        th 'Вот и узнаем.'
        'Закончил я диалог с голосом уже изучая \"внутренности\" под капотом.'
        
        scene avtoda with dissolve
        
        lx 'Ну-с, сейчас посмотрим.'
        'Я начал копаться в самых ответственных местах, которые отвечают за работу машины. {w}Закончив осмотр, я поставил диагноз.'
        lx 'Свечи ни к черту, а так вроде всё хоккей.'
        'Саня и Гошан удивлённо на меня посмотрели.'
        cn 'Ну ты и автомеханик. Ты как это вообще определил?'
        iv '{i}О, Лёшенька, а ты сам-то помнишь?{/i}'
        th 'Помню, как же забыть...'

        $ set_mode_nvl()

        'Я тогда в старшей школе учился. В то время у меня ещё были лояльные отношения с родителями.'
        'В тот день мой отец повел меня в гараж к своей ласточке.'
        'Он сказал: {b}Ну что, Лёха? Ты парень уже взрослый, так что в машинах должен разбираться. Сегодня этим и займёмся.{/b}'
        'Тогда я мечтал, что у меня тоже будет машина и я буду на ней ездить со своими друзьями куда-нибудь загород на шашлыки. {w}Мечты… Отец открыл капот своей машины и жестом руки приказал мне подойти.'
        'После чего сказал: {b}Вот, смотри, Лёшка. Это двигатель. Слушай и запоминай! В жизни пригодится, а когда станешь взрослым, мы тебе машину подарим, будешь сам её чинить.{/b}'
        'Несколько часов отец мне объяснял что такое двигатель, что заставляет двигаться машину, что как называется и как чиниться. {w}Я не очень хорошо слушал то, что мне говорили в школе, но слова бати слушал и пытался запомнить всё, что он говорил. {w}И это мне пригодилось.'

        $ set_mode_adv()

        iv '{i}Тогда ты был совсем другим человеком. А сейчас что случилось?{/i}'
        th 'Не я в этом виноват.'
        iv '{i}Ну да. Кто угодно, но не ты.{/i}'
        'Картинка начала размываться и я услышал знакомый голос.'
        cn 'Лёха. Лёха, ты чего завис?! Эй, слышишь меня?'
        iv '{i}Эх. Давай-ка вернемся в реальность, если этот лагерь можно так назвать.{/i}'

        $ set_mode_adv()
        scene bg ext_dining_hall_near_sunset with dissolve

        cn 'Эй. Лёх.'
        'Я потряс головой.'
        lx 'Что ты орёшь?'
        'Саня нахмурился.'
        cn 'А как по-другому? Ты же встал как вкопанный. Я тебя зову, а ты не реагируешь.'
        'Я размял шею, развернулся к машине и закрыл капот.'
        lx 'Просто прошлое вспомнил. Всё окей.'
        'Саня похлопал меня по плечу.'
        cn 'Ладно. {w}Хорошо, вернёмся к машине. Что нам нужно?'
        'Я осмотрелся.'
        lx 'Нам нужны свечи и примитивные инструменты. Думаю, достав всё это, смогу починить эту колымагу. Можем попробовать на ней слинять.'
        'Саня кивнул.'
        lx 'А где Гошан?'

        play ambience ambience_camp_center_evening fadeout 1.5 fadein 1.5

        'Я осмотрелся в поисках Гошана. {w}Долго искать не пришлось, он сидел на скамейке и дремал.'
        'Я потёр глаза и зевнул.'

        stop music fadeout 2

        lx 'Так, пора спать. Санёк, буди Гошана и идите домой, да и я пойду. Всё, спокойной.'
        'Я пожал руку Сане.'
        cn 'Хорошо. Спокойной.'
        'Я двинул от столовой к дорожкам с домиками.'

        scene bg ext_houses_sunset with dissolve

        'Выйдя на нужное направление, я вновь задумался.'
        th 'А ведь сейчас, если бы я встретил отца, то вряд ли сказал бы спасибо. Скорее, послал бы его. Мда.'
        iv '{i}А что насчёт лагеря думаешь?{/i}'
        th 'Не знаю. Я даже не пойму, зачем мы тут. {w}Хотя место неплохое, красивое, спокойное. {w}Так, стоп. {w}Нет, лагерь точно нечист. Отсюда нельзя уйти, это тюрьма настоящая.'

        play sound sfx_hiding_in_bush

        'Я бы и дальше рассуждал об этом, но вдруг в ближайших кустах что-то зашуршало и промелькнул силуэт.'

        stop sound fadeout 1.0

        th 'Что за чёрт?'
        'Шуршание длилось недолго и быстро стихло, а силуэт исчез.'

        if ((day1_wentToLibrary) and (day1_helpedLena == False)):
            th 'Видимо, шпион сбежал. {w}Хм, шпион... Может, это опять Ульяна была? После событий в библиотеке можно сделать вывод, что она любит кусты. {w}Ладно, плевать.'
        else:
            th 'Не знаю, кто или что это было, но оно убежало. Надеюсь, что на оставшемся пути к дому такого не будет. {w}Ладно, плевать.'

        scene bg ext_house_of_mt_sunset with dissolve

        'Пройдя ещё немного по дорожке, я вышел к месту, где  буду спать... вместе с вожатой.'
        th 'Чёрт. Я совсем забыл, что я в одном доме с вожатой. {w}Ладно, разберёмся.'
        'Подойдя к двери, я понял, что лучше постучаться. А то проблем будет много, если я ворвусь, а она там переодевается.'

        play sound sfx_knock_door2

        'Сделав пару ударов в дверь, я услышал женский голос.'

        stop sound
        mt 'Открыто, заходите.'

        scene bg int_house_of_mt_sunset with dissolve
        play sound sfx_open_door_1

        'Я вошёл в дом. {w}Ольга, увидев меня, заулыбалась.'

        stop sound
        play ambience ambience_camp_center_evening fadein 1.5

        mt 'А, Лёша. Привет, привет. Ну, как день прошёл? Что видел, с кем познакомился?'
        'Я был очень уставший и рассказывать о том, как провёл день, мне не хотелось. {w}Пришлось придумать самый лучший ответ, какой можно мог быть.'
        lx 'Всё видел, со всеми познакомился. Ольга Дмитриевна, я очень устал. Можно я лягу спать?'

        stop music fadeout 2

        'И не дожидаясь ответа Ольги, я быстро разделся и лёг на кровать.'
        'Засыпая, на фоне я неразборчиво слышал голос соседки по комнате, но мне было пофиг.'

        stop ambience fadeout 1.0
        show blink

        'Я практически сразу заснул.'

        scene black

        $ night_time()
        play music music_list['faceless'] fadein 2
        scene prologue_kitchen with dissolve
        show prologue_dream

        'Я сижу на кухне. {w}На столе скомканная пачка сигарет, рядом с ней чашка чая.'

        play sound '/interestingNumber/sound/kashelda1.mp3'

        'Делаю затяжку, кашляю.'

        stop sound

        th 'Вот почему, если доверяешься кому-то, то тут же получаешь нож в спину? И причём ведь второй раз на эти грабли наступаю!'

        play sound sfx_table_hit

        'Бью по столу. {w}Тут я понимаю, что вижу себя со стороны.'

        stop sound

        lx 'Что за чёрт? Это сон? Что происходит?'
        iv '{i}Это ты мне расскажи, что тут происходит?{/i}'
        lx 'Ах да...'

        $ set_mode_nvl()

        'Началось это всё полтора года назад, в одиннадцатом классе.'
        'Тогда я получил первый удар. {w}Я, как типичный подросток, не думал о будущем. Жил, как говорится, одним днём. {w}Учёба меня не парила, единственное, что я учил - это мою любимую гитару. Ну, на тот момент я мучал только отцовскую.'
        'Моё отношение к учебе не прошло бесследно. {w}Успеваемость падала, родителям всё время звонили со школы с одной и той же претензией. На вопрос родителей {b}\"Почему всё так?\"{/b} я отвечал честно, что мне это всё неинтересно и вообще, я хочу стать музыкантом.'

        $ set_mode_adv()

        iv '{i}Видимо, не срослось?{/i}'
        lx 'Хах, да, не получилось. Тогда я ещё не знал, что для этого нужен хоть какой-то капитал и хоть какие-то связи.'
        iv '{i}Так почему же ты жертва в этой истории?{/i}'
        lx 'Тут нет однозначно злодея или героя....'

        $ set_mode_nvl()

        'Из-за того, что я отказывался вразумиться и слушать родителей, отец решил со мной не мараться и, поставив на мне крест, сдал меня в жёлтый дом. {w}Не знаю, почему они это сделали, но, возможно, потому что я мыслил не как они. Для меня это было подлое предательство, удар исподтишка.'
        'Ведь родителям я верил и любил их, несмотря на многие небольшие ссоры. {w}Я помню слова отца: {b}Лёша, слушай, я понимаю, что тебе сейчас непросто в жизни. Определиться с будущем, особенно в твоём возрасте, сложно. Мы с твоей мамой заказали сеанс у психолога. Давай съездим к нему, ты с ним поговоришь, может, он поможет, согласен? Пожалуйста, Лёш...{/b}'
        'Я, дурак, согласился. Уехал отец от {b}\"психолога\"{/b} один, а я там остался на месяц. {w}На адский месяц. {w}Мучали меня не физически, а {b}\"препаратами, которые помогут тебе, Алексей\"{/b}. Отходил я от этих таблеток ещё месяц после выписки. Я бы пробыл там и дольше, если бы не подыграл врачам.'
        'Благо, они поверили. Реабилитировавшись, я осознал, что жизнь теперь у меня будет другая. {w}С тех пор и началась у меня такая, своего рода, война с родаками и принятием того, что даже самые близкие люди могут предать.'
        'Школу я всё же окончил и смог кое-как сдать ЕГЭ. {w}Когда поступил в универ, родители, видимо, осознав, что они тогда сделали, купили мне квартиру.'

        $ set_mode_adv()

        iv '{i}Так, понятно. Это первое предательство. Но мы ведь не в том времени. Что происходит именно сейчас?{/i}'
        iv '{i}Почему ты сидишь на кухне, куришь и кого-то проклинаешь?{/i}'
        lx 'Да... Если я ничего не путаю, то буквально час назад я получил снова нож в спину...'

        $ set_mode_nvl()

        'Мы с нашей компании одногруппников гуляли по парку, типичный вечер первокурсников. {w}Я был, как мне тогда казалось, среди друзей. Хотя там был один пацан из богатой семейки, который много о себе думал. {w}Характерами мы не сошлись, он не любил критику в свою сторону, а я не любил его отношение к людям.'
        'Парень этот был уже подвыпивший и начал приставать к одной из девушек в нашей компании. {w}Поначалу это выглядело как тупая шутка, но дальше переросло в обычное домогательство. Но никто не хотел вмешаться, так что пришлось мне. {w}Конечно, этому золотому мальчику не понравилось, что его останавливают, так ещё и осуждают его действия.'
        'Я был готов к драке, но он позвал за гаражи, так сказать, {b}\"поговорить\"{/b}. {w}Дальше случилось то, чего я не ожидал. Он достаёт нож, но не нападает на меня, а протыкает себя, после чего начинает вопить что есть мочи.'
        'Когда все прибежали, тот урод начал меня обвинять,  никто не захотел слушать мои слова и те, кто были мне, как я считал, друзьями, перешли на сторону этого парня. Даже та девушка, к которой он приставал.'
        'Они захотели преподать мне урок, которого я не заслужил. Хоть я и хотел остаться, попытаться дать отпор, но инстинкт самосохранения взял верх и я убежал. {w}Убежал домой. А теперь сижу на кухне и курю.'

        $ set_mode_adv()

        iv '{i}Это конец?{/i}'
        lx 'Нет, смотри дальше.'
        'В моем кармане завибрировал телефон.'
        iv '{i}И?{/i}'
        lx 'Сообщение от того урода.'
        'Я быстро пробежался глазами по тексту сообщения.'
        th 'Не переходи мне дорогу, придурок. Но, знаешь, за такое смешное представление я тебя пока {b}прощу{/b}. О, и не волнуйся, я в порядке.'
        'Я чувствовал, как налились кровью мои глаза. {w}Если бы не сигареты, то я точно что-нибудь сломал бы.'
        th 'Вот сучара. И почему жизнь - это игра про мудаков и предателей?'
        'Я докурил сигарету, выпил чай и отправился к кровати.'

        show blink

        'Уложившись на нее, я просто закрыл глаза.'
        'Картинка начала плыть.'

        stop music fadeout 2

        iv '{i}Да, увидимся в следующем сне.{/i}'
        lx 'Да?'

        scene black
        show unblink
        hide prologue_dream

        'Я хотел что-то спросить, но в этот момент проснулся.'
        
        # jump ourday2